@echo off

REM Initialize

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=console
iff %# gt 0 then
  set command=%1
  shift
endiff

if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 
firewall [console|panel](console)
  log [<host>](localhost): Display the firewall log for host.
  rule add|delete|status|enable|disable <name>: Firewall rules for host.
  enable|disable|status - control firewall status
  setup - set default configuration
endtext
quit 1

:panel
iff "%@search[firewall.cpl]" != "" then
	start /pgm firewall.cpl
endiff
return

:console
iff "%@search[wf.msc]" != "" then
	start /pgm "%SystemRoot%\system32\WF.msc"
else
	gosub panel
endiff
return

:log

gosub GetHost
if %# != 0 goto usage

call host prepare %host
if %? != 0 return

set LogFile=\\%host\admin$\system32\LogFiles\Firewall\pfirewall.log
iff not exist "%LogFile" then
  set LogFile=\\%host\admin$\pfirewall.log
endiff

iff exist "%LogFile" then
	call TextEdit "%LogFile"
else
	echo The firewall log file does not exist on %host.
endiff

return

:GetHost

set host=%ComputerName
iff %# gt 0 then
	set host=%1
	shift
endiff

return

:rule

if %@IsNewOs[] == 0 return 1

set command=status
iff %# gt 0 then
	set command=Rule%1
	shift
endiff
if not IsLabel %command goto usage

if %# == 0 goto usage
set name=%@quote[%1]
shift

gosub %command
return %_?

:RuleStatus
if %# != 0 goto usage
netsh advfirewall firewall show rule name=%name
return %?

:RuleDelete

if %# != 0 goto usage

iff %@FirewallRuleExist[%name] == 1 then
	echos Deltinging firewall rule %name...
	netsh advfirewall firewall delete rule name=%name
endiff

return

:RuleAdd

set rule=%$
if "%rule" == "" goto usage

REM If the rule already exists update it
set command=add
set new=
iff %@FirewallRuleExist[%name] == 1 then
	 set command=set
	 set new=new
else
endiff

REM Add the rule
echos Adding firewall rule %name...
netsh AdvFirewall firewall %command rule name=%name %new %rule

return

:RuleEnable
:RuleDisable

if %# != 0 goto usage

iff %@FirewallRuleExist[%name] == 1 then
	echos Enabling firewall rule %name...
	netsh advfirewall firewall set rule name=%name new enable=%@if[ %command==RuleEnable ,yes,no]
endiff

return

:enable
netsh advfirewall set currentprofile state on
return %?

:disable
netsh advfirewall set currentprofile state off
return %?

:status
netsh advfirewall show currentprofile
return %?

:setup

iff %@IsNewOs[] == 1 then

	echos Enabling dropped packet logging for all profiles...
	netsh advfirewall set allprofiles logging droppedconnections enable

elseiff %@IsXp[] == 1 then
	
	text
- Exceptions
  - Check File and Printer Sharing and Remote Desktop
- Advanced
  - Security Logging Settings..., check Log dropped packets
  - ICMP Settings..., check Allow incoming echo requests
- Notes
  - XP firewall will block psexec rpc connections causing slow response
	endtext
	call firewall console
	pause
	
endiff

return
