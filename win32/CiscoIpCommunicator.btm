@echo off
SetLocal

REM Initialize
set title=Cisco IP Communicator
set program=%programs32\Cisco Systems\Cisco IP Communicator\communicatork9.exe
set HeadsetLink="%programs\WIDCOMM\Bluetooth Software\BTWUIExt.exe" /deviceAddr=00237faa1498
set HeadsetTitle=Voyager PRO+
	
REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=start
iff %# gt 0 then
  set command=%1
  shift
endiff
if not IsLabel %command goto usage

if %# != 0 goto usage

REM Run command
gosub %command
quit %_?

:usage
text
usage: CiscoIpCommunicator [start|startup|close|headset](start)
endtext
quit 2

:headset
echo Turn on the headset and do not close the devcice control window.
start /pgm %HeadsetLink
sleep 2
activate "%HeadsetTitle" MIN
return

:start
:startup

REM Return if the program is  not installed or already running
if not IsFile "%program" return
if %@IsTaskRunning[%@name[%program]] == 1 return 0

REM Start the phone program
pushd %@path[%program]
call task start dup title "%title" "%program"
popd

return

:close
call task CloseKill title "%title" "%program"
return

:IsInstalled
return %@if[IsFile "%program",1,0]

:IsRunning

gosub IsInstalled
if %_? == 0 return 0

return %@IsTaskRunning[%program]