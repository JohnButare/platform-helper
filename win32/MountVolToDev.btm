@echo off

REM Initialize
set DevDir=c:\dev
set NamesShown=

set IgnoreList=\\?\Volume{773b6c38-10c8-11e1-860e-000272acc93f}\ ^
	\\?\Volume{773b6c3c-10c8-11e1-860e-000272acc93f}\ ^
	\\?\Volume{773b6c32-10c8-11e1-860e-000272acc93f}\ ^
	\\?\Volume{979f5099-152f-11e1-9fa7-000272acc93f}\	^
	\\?\Volume{cecd2f0d-a7f7-11e0-961c-005056c00008}\ ^
	\\?\Volume{cecd2f0f-a7f7-11e0-961c-005056c00008}\

REM Create devDir if it does not exist
call MakeDir "%DevDir"

REM Remove c:\dev directories that are not mount points
for /h /a:d %dir in (%DevDir\*) if %@wattrib[%dir,J,p] == 0 rd %dir

REM Add new mount points
echos Checking...
for %drive in (%_drives) gosub ProcessDrive
echo done.

quit 0

:ProcessDrive

echos .

REM Remove the colon
set drive=%@left[-1,%drive]

REM Process non-netork drives that are ready, a cdrom, or removable.
if %@remote[%drive] == 1 return
if not (%@ready[%drive] == 1 .or. %@cdrom[%drive] == 1 .or. %@removable[%drive] == 1) return

REM Check labels
set DriveLabel=
if %@ready[%drive] == 1 set DriveLabel=%@trim[%@label[%drive]]
if "%DriveLabel" == "data" return

REM Get the volume name for the drive, returning if we got an error trying to get the volume name
set VolumeName=%@execstr[mountvol %drive:\ /l]
if %? == 1 return

REM Return if we are ignoring the volume
if %@IsInList[%VolumeName %IgnoreList] == 1 return

REM See if drive is already mounted
set found=
for /h /a:d %dir in (%DevDir\*) if "%volumeName" == "%@execstr[mountvol %dir /l]" set found=true
if defined found return
if "%drive" == "c" return

REM Get mount name
iff "%@left[6,%DriveLabel]" = "backup" .or. "%@left[5,%DriveLabel]" = "media" .or. "%@left[2,%DriveLabel]" = "sd" .or. ^
	"%@left[8,%DriveLabel]" = "portable" .or. "%@left[6,%DriveLabel]" = "router" then
	
	set DriveName=%@lower[%DriveLabel]

elseiff "%drive" == "A" then
	set DriveName=floppy

else
	gosub ShowNames
	echo.
	input `Name for drive `%drive`? ` %%DriveName
endiff

if "%DriveName" == "" return

REM Need to be elevated to actually mount
iff %@IsElevated[] == 0 then
	call sudo MountVolToDev
	quit %?
endiff

REM Mount drive
echo Mounting drive %drive to %DevDir\%DriveName...
call MakeDir "%DevDir\%DriveName" 
MountVol "%DevDir\%DriveName" %VolumeName

REM Drive specific setup

iff "%@left[5,%DriveLabel]" == "media" then
	echo - Right click on each media folder and select Include in library
	call explorer.btm "%drive"
	pause
endiff

return

:ShowNames

if defined NamesShown return
set NamesShown=true

text


Suggsted names: 

cardN - card reader
opticalN - optical drives
portableN - portable hard drive
backupN - backup hard drive
isoN - ISO drive
key - USB key drive
endtext

return