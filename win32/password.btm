@echo off
setlocal

REM Initialize

REM Get arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=change
iff IsLabel %1 then
	set command=%1
	shift
endiff
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 1>&2
usage: password [change](change)
  change [<host>,...|all](current host) [<UserName>](current user) <NewPassword>
endtext
quit 1

REM To change domain password, ctrl-alt-del / ctrl-win-del (VMware) / ctrl-alt-end (Remote Desktop) and click Change Password
:change

set host=%ComputerName
set user=%UserName
set password=

iff %# == 3 then
	set host=%1
	shift
endiff

iff %# gt 1 then	
	set user=%1
	shift
endiff

if %# != 1 goto usage

set password=%1
shift

iff "%host" == "all" then
	REM Change password on other active hosts first so we can access them with the current password
	set host=%@replace[ ,`,`,%@trim[%@ActiveHost[all,platform=win] %ComputerName]]
endiff

iff %@OnNetwork[Wiggin] == 1 then

	input /p `Enter `%UserName` LDAP password: ` %%LdapPassword
	call ssh nas ldappasswd -x -D "uid=%UserName,cn=users,dc=butare,dc=net" -w "%LdapPassword" -s "%password" "uid=%user,cn=users,dc=butare,dc=net"
	if %? != 0 return %?
	echo %user@butare.net password successfully changed.
	echo.

	call ssh root@nas /usr/syno/sbin/synouser -setpw %user %password
	if %? != 0 return %?
	echo NAS\%user password successfully changed.
	echo.

endiff

call sudo %@if[ %@IsElevated[] == 0,/wait] pspasswd "\\%host" %user %password
return %?
