@echo off
SetLocal

if %# lt 2 goto Usage

set SrcDir=%1
set DestDir=%2
set AccountList=%3$

set SrcMachine=%@Word["\",0,%@strip[^q,%SrcDir]]
set DestMachine=%@Word["\",0,%@strip[^q,%DestDir]]

set RcOpts=/E /V /R:5 /W:1 /SecFix /xd _borders _cusudi _fpclass _private _vti*

REM Verify src and dest exist and copy files and permissions
RoboCopy "%SrcDir" "%DestDir" %RcOpts
iff %? gt 8 then
	echo ERROR: RoboCopy failed.
	goto Done
endiff

for %%account in (%AccountList) gosub FixPermissions
goto Done

REM 
REM FixPermissions
REM 
REM Fix the permissions on the destination for the specified account
REM by changing SID on the destination directory of the source machine 
REM SID to the destination machine SID.
:FixPermissions

REM Remove quotes from account name
set account=%@Strip[^q,%account]

REM Parse account as <src account>:<dest account>
iff %@Words[":",%Account] == 1 then
	set SrcAccount=%Account
	set DestAccount=%Account
else
	set SrcAccount=%@Word[":",0,%Account]
	set DestAccount=%@Word[":",1,%Account]
endiff

REM If account is IUSR get the name of the IUSR on the src and dest machines
iff "%Account" == "IUSR" then
	set SrcAccount=%@Word[0,%@ExecStr[RunScript c:\Data\bin\IisAccountInfo.vbs %SrcMachine]]
	set DestAccount=%@Word[0,%@ExecStr[RunScript c:\Data\bin\IisAccountInfo.vbs %DestMachine]]
endiff

REM If account is IWAM get the name of the IWAM on the src and dest machines
iff "%Account" == "IWAM" then
	set SrcAccount=%@Word[1,%@ExecStr[IisAccountInfo %SrcMachine]]
	set DestAccount=%@Word[1,%@ExecStr[IisAccountInfo %DestMachine]]
endiff

REM Get the source and destination SID's using getsid.
set TempFile=%@unique[%Temp]
getsid \\%SrcMachine "%SrcMachine\%SrcAccount" \\%DestMachine "%SrchMachine\%DestAccount" > "%TempFile"
iff %? != 0 then
	echo ERROR: Unable to get SID information for account %Account.
	cat "%TempFile"
	goto Done
endiff

REM Parse the source and destination SID's from the getsid output.
set SrcSid=%@word[-0,%@line[%TempFile,1]]
set DestSid=%@word[-0,%@line[%TempFile,2]]

REM If the source and desintation SID's are the same then we are done.
iff "%SrcSid" = "%DestSid" then
	echo FINISHED: Account %Account SID's already match.
	goto DoneFixPermissions
endiff

echo CHANGING: %SrcAccount `->` %DestAccount
echo    %SrcSid `->` %DestSid

REM Change permissions
subinacl /file "%DestDir" /replace=%SrcSid=%DestSid
subinacl /subdirectories "%DestDir\." /replace=%SrcSid=%DestSid
echo.

:DoneFixPermissions
rm -f %TempFile
return

:Usage
Text
USAGE: %0 <SourceDirUnc> <DestDirUnc> [account1 account2 ...]
   - Copies files and permissions from SourceDir to DestDir.
   - Optionally, the SID's for one or more local accounts are replaced with 
     the SID of the account on the destination server.
   - The account name has the format <account name> or 
     <source account name>:<destination account name>
   - Account names IUSR and IWAM are treated specially.  They are translated 
     to the name of the IUSR or IWAM account on the source and destination
     computers.
EndText
goto Done

:Done
