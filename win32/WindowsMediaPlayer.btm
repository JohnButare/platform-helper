@echo off
SetLocal

REM Initialize
set program32=%programs32\Windows Media Player\wmplayer.exe
set program64=%programs64\Windows Media Player\wmplayer.exe
set program=%program32

set title=Windows Media Player

REM Exit if Windows Media Player is not installed
iff not exist "%program" then
  quit 0
endiff

REM Initialize arguments
set command=start
set bits=

REM Get arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

:GetArgs

iff IsLabel %1 then
	set command=%1
	shift
	goto GetArgs
endiff

iff "%1" == "32" .or. "%1" == "64" then
	set bits=%1
	set program=%@EvalVar[program%bits]
	shift & goto GetArgs
endiff

REM Run command
if not IsLabel %command goto usage
gosub %command
quit %_?

:start

iff %@IsTaskRunning[%@name[%program]] == 0 then
	call task start %idle wait title "%title" "%program"
else
	gosub show
endiff

return

:startup

if %@IsTaskRunning[wmplayer] == 1 return 0

REM Force 32 bits for WhiteCap visualizer
call task start %idle min wait title "%title" "%program32"

gosub play

activate "%title" min

return

:restart
gosub close
call task start wait "%program"
return

:show

REM Activate shows icon, run the program to show it
"%program"

return

:close
iff %@IsTaskRunning[wmplayer] == 1 then
	REM task close wait wmplayer did not work under Vista
	process.exe -q wmplayer.exe
endiff
return

:usage
text
Control Windows Media Player.
usage: wmp [32|64] [start|close|restart|startup|play|show [<playlist>]|service|sync|SyncPlaylist](start)
  service [start|stop|restart|auto|demand|disable|enable|status](restart) [host]
endtext
quit 1

:play

set playlist=All
iff %# gt 0 then
	set playlist=%1
	shift
endiff

if %# != 0 goto usage

gosub FindPlaylist
if %_? != 0 return %_?

"%playlist"

return

:FindPlaylist

set playlist=%@UnQuote[%playlist]

iff IsFile "%playlist" return 0

REM Ensure the playlist has a wpl extension
iff "%@ext[%playlist]" != "wpl" then
	set playlist=%playlist.wpl
endiff

REM Look for the playlist in the user and public playlist directories
for dir in ("%@UserMusic[]\Playlists" "%@PublicMusic[]\Playlists") ^
(
	iff IsFile "%@UnQuote[%dir]\%playlist" then
		set playlist=%@UnQuote[%dir]\%playlist
		LeaveFor
	endiff
)

iff not IsFile "%playlist" then
	EchoErr Unable to locate playlist %@name[%playlist].
	return 1
endiff

return 0

:service

set command=restart
iff %# gt 0 .and. IsLabel Service%1 then
	set command=%1
	shift & goto ServiceGetArgs
endiff

set host=
iff %# gt 0 then
	set host=%1
	shift
endiff

if %# != 0 goto usage

REM Windows Media Player Network Sharing Service
call service %command wait WMPNetworkSvc %host

return

:IsInstalled
return %@if[IsFile "%program",1,0]

:IsRunning

gosub IsInstalled
if %_? == 0 return 0

return %@WindowExist[%title]

:sync
call wmp start
text
- (Android) USB Conenction, Windows Media Sync
- Sync, Sunc '5 stars' to 'John's Phone'
endtext
return

:SyncPlaylist

text
- In Windows Media Player, click Playlists
- Right click the playlist and select play (Auto Playlists: 5 stars, Best)
- Save list as... (on the drop down to the right of Clear list)
- Save as type-M3u playlist, location D:\Users\Public\Music\Playlists
endtext	
call wmp start
pause

return
