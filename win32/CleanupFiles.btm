@echo off
REM reference: http://www.roger.id.au/tweaks/slimming.php
SetLocal


REM Initialize
on break cancel 1

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=start
iff IsLabel %1 then
	set command=%1
	shift
endiff

REM Run the command
gosub %command
quit %_?

:usage
text 1>&2
usage: CleanupFiles [start|manual](start)
endtext
quit 2

:start

REM Initialize
set debug=
REM set debug=/debug

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

call CleanTemp
gosub CleanFolders
gosub CleanFiles
gosub CleanupRegistry
call os RemoveHotfixBackup
if %@IsInstalled[Firefox] == 1 call firefox cache clear
if %@IsInstalled[Saba] == 1 call saba cleanup
if %@IsInstalled[VirusScan] == 1 call VirusScan cleanup
if %@IsInstalled[Word] == 1 call word RestoreNormal
if %@IsInstalled[iTunes] == 1 call iTunes cleanup
if %@IsInstalled[PictureMotionBrowser] == 1 call PictureMotionBrowser.btm cleanup
call os.btm cleanup

return 0

:CleanupRegistry

echos Cleaning registery...

call registry.btm 32 delete "HKCU\SOFTWARE\Microsoft\Windows\CurrentVersion\Run\MSMSGS"
call registry.btm 64 delete "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Run\NvCplDaemon"
call registry.btm 64 delete "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Run\NvMediaCenter"

call sidebar cleanup
call intel vpn cleanup >& nul:

echo done

return

:manual

echos Manual cleanup report...

REM c:\MSOCache - Microsoft Office installation file cache
REM %WinDir\Downloaded Program Files - ActiveX controls for web sites
REM %WinDir\inf - hardware installation (human interface devices, removable media, etc.) and Windows Components (IIS, etc.).	xp ResroreInfo to restore.
REM %WinDir\Installer - Application installation msi files, used for upgrades, patches, and repair
REM %WinDir\SoftwareDistribution - Windows Update

set ad=
set wd=%WinDir

set files=%UserHome\AppData\LocalLow\*.tmp

set dirs="%LocalAppData\Microsoft\Windows\Explorer" c:\MSOCache ^
	"%wd\%WinDir\Downloaded Program Files" %wd\Installer %wd\inf %wd\SoftwareDistribution ^
	"%UserHome\Local Settings\Temporary Internet Files"
	
setdos /x-3
for file in (%files) (
	setdos /x0
	iff IsFile "%file" then
		echos %@FileName[%file] %@FileSize["%file",m]M...
	endiff
	setdos /x-3
)
setdos /x0

for dir in (%dirs) (
	iff IsDir "%dir"	then
		echos %@FileName[%dir] %@DirSize[h,%dir]...
	endiff
)



echo done
 
return

:CleanFolders

echos Cleaning folders...

REM Updater5 - Adobe Acrobat updater
REM Newsid backup - Registry backup after SID has been changed by the newsid program
REM SoftwareDistribution - http://www.optimizingpc.com/optimize/deletefiles.html

call DelDir %debug NoHeader ^
	"%programs32\Adobe\Acrobat 7.0\Setup Files" "%programs32\Real\RealGames\Download" ^
	"%UserDocuments\updater5" "%WinDir\system32\config\Newsid Backup" "%WinDir\$NTServicePackUinistall$"

call DelDir %debug contents NoHeader "%PublicData\test" "%WinDir\Minidump" ^
	"%WinDir\SoftwareDistribution\Download" "%WinDir\PCHEALTH\ERRORREP\UserDumps"	

REM Virtual machine folders
iff %@IsVirtualMachine[] == 1 then

	REM help - windows help files
	REM Driver Cache\i386 - used when installing new devices
	REM $hf_mig$ - HotFix branch folder, reapplies fix after a service pack
	REM RegisteredPackages - registered installation packages, mostly for Windows Media Player
	REM ServicePackFiles\i384 - service pack files used for system file protection
	REM symbols - locally cached symbol (PDB) files	
	pushd "%WinDir" 
	call DelDir %debug contents NoHeader Cursors "Downloaded Installations" "Driver Cache\i386" help `$hf_mig$` RegisteredPackages Restore srchasst usmt ServicePackFiles\i386 symbols
	
	REM oobe - Delete Out Of Box Experience contents in Windows XP.  In Windows Server 2003 and Vista this folder is protected by Windows File Protection.
	iff %_WinVer == 5.1 then
		call DelDir %debug NoHeader System32\oobe
	endiff
	
endiff

echo ...done

return

:CleanFiles

echos Cleaning files...

REM Most files require elevation to delete
iff %@IsElevated[] == 0 then
	echo skipping
	return
endiff

set files=
set FilesSize=

set bd=%@BatchDir[]
set wd=%WinDir

set files=%files ^
	"%WinDir\memory.dmp" "%WinDir\security\logs\winlogon.log" ^
  "%UserProfile\Application Data\Mozilla\Firefox\Profiles\210c4pyx.default\xmarks.log" ^
	"%LocalAppData\Xmarks\xmarks.log" ^
  "%wd\*.old" "%wd\*.bak" "%bd\*.bak" ^
	"%WinDir\Microsoft.NET\Framework\v2.0.50727\*.log"

set FilesSize=%FilesSize "%wd\*.log" "%wd\*.txt" "%wd\*.tmp" 

call DelFile.btm %debug /log %files
call DelFile.btm %debug /xf "`WindowsUpdate.log` `Terminus.log` `SchedLgU.Txt`" /log /ms 1000 %FilesSize

REM Virtual machine files
iff %@IsVirtualMachine[] == 1 then

	REM system32\dllcache - ~1GB - used by system file protection to replace sytem files when changed.  If removed, WFP will prompt for Windows media.
	REM Windows Server 2003 protects some files in this folder using Windows File Protection, disable deletion for now.
	call DelFile %debug /log ^
		/xf "apps.chm bnts.dll msgr3en.dll nls302en.lex sniffpol.dll srchctls.dll srchui.dll sstub.dll tshoot.dll wmsgr3en.dll wnls302en.lex wsrchctls.dll wsrchui.dll" ^
		"%WinDir\system32\DllCache\*"

endiff
	
echo ...done

return