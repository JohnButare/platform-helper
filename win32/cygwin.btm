@echo off
SetLocal

REM Initialize
on break cancel 1
set InstallDir=%programs32\Cygwin
set bin=%InstallDir\bin

set DownloadSite=http://www.gtlib.gatech.edu
set SetupArgs=`--no-shortcuts --site %DownloadSite --local-package-dir "%PackageDir" --root "%InstallDir"`

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=setup
iff IsLabel %1 then
	set command=%1
	shift
endiff

REM Run the command
gosub %command
quit %_?

:usage
text 1>&2
usage: cygwin [bash|terminal|download|setup|home](setup)
  download - download new or updated Cygwin binaries to the local store
  setup - install new Cygwin binaries from local store
endtext
quit 2

:download

gosub FindSetup
if %_? != 0 return %_?

echo - (update packages), View=Pending, Next
start /D%temp /pgm "%setup" --download %SetupArgs

return 0

:setup
REM http://cygwin.com/faq/faq.setup.html#faq.setup.cli

gosub FindSetup
if %_? != 0 return %_?

start /pgm "%setup" --local-install %SetupArgs

return 0

:FindSetup

call FindPublicDoc data\install\Cygwin\setup\setup.exe %2
if %_? != 0 quit %_?

set setup=%file

set PackageDir=%@parent[%@parent[%setup]]\package
iff not IsDir "%PackageDir" then
	EchoErr Unable to locate the Cygwin package directory.
	return 1
endiff

return 0

:terminal
"%bin\mintty.exe" -
return %?

:bash
iff %# == 0 then
	"%bin\bash.exe" --login -i %$
else
	REM "%bin\bash.exe" --login -c %$
	"%bin\bash.exe" %$
endiff
return %?

:home
cde "%programs32\Cygwin\home\%UserName"
EndLocal /d
return