@echo off
SetLocal

REM Initialize
set MediaExtensions=avi, 3g2,3gp,mov,mp3,mp4,mpe,m4a,m4p,m4v,mpg,mts,m2ts, wma,wmv, ram,rm,rmvb 
set pad=20
set VersionProperties="Product Name" "File Description" "File Version" "Product Version" "Internal Name" "Original Filename" "Company Name" "Legal Copyright" "Legal Trademarks" 
set SummaryProperties=Title Subject Author Keywords Comments Template LastAuthor "Revision Number" "Edit Time" "Last printed" Created "Last Saved" "Page Count" "Word Count" "Char Count" AppName

REM Arguments
if %# != 1 goto usage
set file=%@UnQuote[%@full[%1]]

iff not IsFile %@quote[%file] then
  echo %file was not found.
  quit 1
endiff

set FileExtension=%@ext[%file]
set IsExecutable=%@if[ %@IsExecutable[%file] == 1 ,true,]

REM Show version information
gosub ShowVersion

REM Executable information
if defined IsExecutable gosub ShowExeInfo

REM Signature
if defined IsExecutable .or. "%FileExtension" == "msi" .or. "%FileExtension" == "msu" gosub ShowSignature

REM Show summary information - not functional for new Office document formats
iff not defined IsExecutable .and. "%FileExtension" != "sys" then
	gosub ShowSummary
endiff

REM
REM Show information for specific files types
REM

REM Media
iff %@IsInList[%FileExtension %MediaExtensions] == 1 then

	REM Display media information
	REM Alternatives: mp4box -info 
  MediaInfo %@quote[%file]

endiff

REM MP3
iff "%FileExtension" == "mp3" then
	echo Music: 
  tag %@Quote[%file]
endiff

quit 0

:ShowSignature

echo.
echo Signature:
sigcheck.exe -q -i "%file" | egrep -v "%@FileName[%file]" | egrep -v "Publisher:" | egrep -v "Product:" | egrep -v "Version:" | egrep -v "File version:" | egrep -v "Description:"
return

:ShowExeInfo

iff %@IsNet[%file] == 1 then
	set header=.NET
	gosub show Version "%@NetVersion[%file]"
	gosub show "Runtime Version" "%@NetRuntimeVersion[%file]"
	gosub show "Framework Version" "%@NetFrameworkVersion[%file]"
	gosub show "Target" "%@NetTarget[%file]"
endiff

set header=Executable
gosub show Architecture "%@ExeArchitecture[%file]"
gosub show Subsystem "%@ExeSubsystem[%file]"

REM FileVer - http://support.microsoft.com/kb/913111
set FileVer=%@ExecStr[filever /a /d "%file"]
gosub show "header" "%@Word[0,%FileVer]"
gosub show "type" "%@Word[1,%FileVer]"
gosub show "language" "%@Word[2,%FileVer]"
gosub show "version" "%@Word[3,%FileVer]"
gosub show "build" "%@Word[4,%FileVer]"

return

:ShowSummary

set header=Summary

for %prop in (%SummaryProperties) (
	on error REM Suppress function errors
	set value=%@summary["%file",%prop] >& nul:
	gosub show %prop "%value"
	on error
)

return

:ShowVersion

set header=Version

for %PropText in (%VersionProperties) (
	on error REM Suppress function errors
	gosub show %PropText "%@VerInfo["%file",%@replace[ ,,%PropText]]"	
	on error
)

return

:show [label value]

if %@quote[%value] == "" .or. %@quote[%value] == "-" return

iff defined header then
	echo.
	echo %header:
	set header=
endiff
		
echo %@pad[%pad,%@UnQuote[%label]]: %@trim[%@UnQuote[%value]]

return
	
:usage
text 1>&2
Show file information.
FileInfo <file>
endtext
quit 1
