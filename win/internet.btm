@echo off
SetLocal
on break cancel 1

REM Initialize
set key=HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=options
iff IsLabel %1 then
	set command=%1
	shift
endiff

REM Run the command
gosub %command
quit %_?

:usage
text 1>&2
usage: internet [options|proxy](options)
  proxy get|set [site](detect) - set or get proxy configuration
endtext
quit 2

:options
start /pgm rundll32.exe shell32.dll,Control_RunDLL inetcpl.cpl,,4
return

:proxy

set command=get
iff %# gt 0 then
	set command=%1
	shift
endiff
if not IsLabel Proxy%command goto usage

gosub Proxy%command
return %_?

:ProxyGet
set AutoConfigURL=%@RegGet["%key\AutoConfigURL"]

if "%AutoConfigURL" != "" echo Automatic configuration script=%AutoConfigURL
	
iff %@RegGet[%key\ProxyEnable] == 0x1 then
	echo Proxy is enabled.
	echo Proxy Server=%@RegGet[%key\ProxyServer]
	if "%@RegGet[%key\ProxyOverride]" != "" echo Proxy Override=%@RegGet[%key\ProxyOverride]
else
	echo Proxy is disabled.
endiff

return

:ProxySet

REM Initialize
set proxy=

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set proxy=
iff %# gt 0 .and. IsLabel ProxySet%1 then
	set proxy=%1
	shift
endiff

if %# != 0 goto usage

REM Discover the network
iff not defined proxy then
	iff %@OnNetwork[Intel 2000] == 1 then
		set proxy=%@NetworkSite[]
		if not IsLabel ProxySet%proxy set proxy=Intel
	else
		set proxy=None
	endiff
endiff

REM Windows Operating System
echos Setting Internet Explorer proxy to %proxy...
gosub ProxySet%proxy
echo done

REM DropBox
REM call DropBox proxy %@if[ "%proxy" == "None" ,off,on]

return

:ProxySetAuto
gosub ProxySetNone
call registry.btm set "%key\AutoConfigURL" REG_SZ http://autoproxy >& nul:
return %?

REM Intel automatic proxy configuration scripts - http://wpad.intel.com/wpad.dat, http://autoproxy.intel.com, http://autoproxy
:ProxySetIntel
gosub ProxySetNone
call registry.btm set "%key\AutoConfigURL" REG_SZ http://autoproxy.intel.com:9090 >& nul:
return %?

:ProxySetNone
call registry.btm delete "%key\AutoConfigURL"
call registry.btm set "%key\ProxyEnable" REG_DWORD 0 >& nul:
return %?

:ProxySetIntelCommon
gosub ProxySetNone
call registry.btm set "%key\ProxyEnable" REG_DWORD 1 >& nul:
call registry.btm set "%key\ProxyOverride" REG_SZ *.intel.com >& nul:
return

:ProxySetIntelChHold
call registry.btm set "%key\ProxyServer" REG_SZ proxy-ch.intel.com:911 >& nul:
return %?

REM recognition site requires autoproxy, but sharepoint initial load is slower with autoproxy
:ProxySetIntelNmHOLD
gosub ProxySetIntelCommon
set host=proxy.rr.intel.com
set host=rrintpr02.rr.intel.com
call registry.btm set "%key\ProxyServer" REG_SZ %host:911 >& nul:
return %?

:ProxySetIntelFmHOLD
gosub ProxySetIntelCommon
call registry.btm set "%key\ProxyServer" REG_SZ proxy.fm.intel.com:911 >& nul:
return %?