@echo off
REM Out: SharedDocuments, file

REM Initialization
set SearchLocal=true
set SearchHosts=

set LocalDirs="%PublicDocuments"
set RemoteDirs=\\%%host\Public\documents
set NasDir=\\butare.net@ssl@5006\DavWWWRoot\public\documents

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

REM reset argument - if specified, delete the cached value of the SharedDocuments location and exit
iff "%1" == "reset" then
  set SharedDocuments=
	echo The Shared Documents location has been reset.
	
	shift
	if %# == 0 quit 0
	
endiff

set file=%@unquote[%1]
shift

REM search argument - if search is specified, delete the cached value of the SharedDocuments location.
iff "%1" == "search" then
  set SharedDocuments=
  shift
endiff

REM select argument
set select=
iff "%1" == "select" then

  set select=true
  
  REM Do not use the cached shared documents variable
  set SharedDocuments=
  
  REM Create a file to store found locations
  set FoundFile=%@unique[%temp]
  echos. > "%FoundFile"
  
  shift
endiff

REM host argument
iff %# == 1 then
  set SharedDocuments=
  set SearchLocal=
  set SearchHosts=%@UnQuote[%1]
  shift
else

  set SearchAttribute=install
  iff %# != 0 then
    set SearchAttribute=%1
    shift
  endiff
  
endiff

if %# != 0 goto usage

REM If we already have located a shared documents folder, see if the file exists in it.
iff "%SharedDocuments" != "" then
  iff exist "%SharedDocuments\%file" then
    set file=%SharedDocuments\%file
    quit 0
  endiff
endiff

REM Find the hosts to search
iff "%SearchHosts" == "" then
  echos Finding %SearchAttribute hosts...
  set SearchHosts=%@FindHost[%SearchAttribute=yes]
  echo %SearchHosts
endiff

iff "%@FileName[%file]" == "" then
	echos Finding %@FileName[%@parent[%file]]
else
	echos Finding %@FileName[%file]
endiff

if defined SearchLocal gosub CheckLocal
gosub CheckHosts

set SharedDocuments=

iff defined select then
	if exist "%NasDir\%file" echo %NasDir >> "%FoundFile"
	iff %@FileSize[%FoundFile] != 0 then
		set SharedDocuments=%@select[%FoundFile,5,10,20,60,Select Installation]
	endiff
elseiff exist "%NasDir\%file" then
	set SharedDocuments=%NasDir
endiff

iff "%SharedDocuments" == "" then
  echo. 
  EchoErr %file not found.
  quit 1
else
  goto found
endiff

return

:CheckDirs

for %dir in (%dirs) (

	iff exist "%@UnQuote[%dir]\%file" then
		set SharedDocuments=%@UnQuote[%dir]
		gosub AddFound
		if not defined select goto CheckDirsDone
	endiff
	
)

:CheckDirsDone
return

:CheckLocal

echos ...local
set dirs=%LocalDirs
gosub CheckDirs

set dirs=%_ready
gosub CheckDirs

return

:CheckHosts
for %%host in (%SearchHosts) gosub CheckHost
return

:CheckHost

echos ...%host
iff %@IsHostAvailable[%host] == 1 then

	REM Call TCC to fail when run from jjbutare-mobl4
  REM call host prepare %host
  REM if %? != 0 return

	set dirs=%RemoteDirs
	gosub CheckDirs

endiff

return

:AddFound

echos *
iff defined select then
  echo %SharedDocuments >> "%FoundFile"
else
  goto found
endiff

return

:found
echo ...found at %SharedDocuments
set file=%SharedDocuments\%file
quit 0

:usage
text
usage: FindPublicDoc [reset] <file> [search [<host attribute>]|select|<hosts>](search install)
  file - name of the shared file to find
  select - Select from a list of available hosts
  <host> - Specify the host to find the shared documents

return:
  SharedDocuments: set to the location of the shared documents folder
  file: set to the full path of the found file

endtext
quit 1
