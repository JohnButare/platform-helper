@echo off
SetLocal
on break cancel 1

REM Initialize
on break cancel 1
set program=%programs32\Google\Picasa3\Picasa3.exe
set title=Picasa 3

REM Profile
set ProfileApp=Picasa
set ProfileMethod=%LocalAppData\Google\Picasa2
set ProfileFiles=*

REM Arguments
set command=start
set option=
set flag=

if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

:GetArgs

iff %# gt 0 .and. IsLabel %1 then
	set command=%1
	shift & goto GetArgs	
endiff

iff "%1" == "/o" .or. "%1" == "/option" then
	set option=%2
	shift 2 & goto GetArgs
endiff

iff "%1" == "flag" then
	set flag=true
	shift & goto GetArgs
endiff

REM Run the command
gosub %command
quit %_?

:usage
text 1>&2
usage: picasa [start|startup|close](start)
  profile dir|SaveDir|backup|restore [<profile name>|default](latest)
endtext
quit 2

:profile
call profile.btm %$
if %? == 1 goto usage
EndLocal /d
return %?

:start
gosub IsInstalled & if %_? == 0 return
gosub IsRunning & if %_? == 1 return
start /pgm "%program"
return

:startup
gosub IsInstalled & if %_? == 0 return
gosub IsRunning & if %_? == 1 return
call task start min %idle title "%title" "%program"
return 

:close
gosub IsRunning & if %_? == 0 return
process.exe -q "%@FileName[%program]"
return

:IsInstalled
return %@if[IsFile "%program",1,0]

:IsRunning
gosub IsInstalled & if %_? == 0 return 0
return %@IsTaskRunning[%program]