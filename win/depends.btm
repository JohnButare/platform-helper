@echo off
SetLocal

REM Get arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

iff %# == 0 then
	start /pgm RunDepends
	quit 0
endiff

if %# != 1 goto usage

set file=%@search[%@UnQuote[%1]]
iff "%file" == "" then
	echo %1 was not found.
	quit 1
endiff

REM Determine the architecture of the executable
set platform=%@ExePlatformBits[%file]

iff "%platform" == "" then
	echo Could not determine %@FileName[%file] architecture.
	quit 1
endiff

set program=%@BatchDir[]\..\%platform\RunDepends.exe

iff not IsFile "%program" then
	echo Depends is not available for %platform.
	quit 1
endiff

REM Run the depends that matches the architecture of the file
echo Starting %platform depends for %@FileName[%file]...
start /pgm "%program" "%file"

quit 0

:usage
text 1>&2
Show executable dependencies.
depends <file>
endtext
quit 1
