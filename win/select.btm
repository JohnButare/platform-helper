@echo off
SetLocal

REM Initialize

REM Arguments
set command=file
set prompt=true

if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

:GetArgs

iff %# gt 0 .and. IsLabel %1 then
	set command=%1
	shift & goto GetArgs	
endiff

iff "%1" == "/NoPrompt" then
	set prompt=
	shift & goto GetArgs
endiff

if %# != 1 goto usage
set pattern=%@UnQuote[%1]
shift

REM Run the command
gosub %command
quit %_?

:usage
text 1>&2
usage: select [file|dir](file) [/NoPrompt] <pattern>
endtext
quit 1

:file

set name=%@FileName[%pattern]

iff %@files["%pattern"] == 0 then
	EchoErr Unable to locate any %name files.
	return 1
elseiff %@files["%pattern"] == 1 then
	set file=%@FindFirst["%pattern",-d]
else
	if defined prompt pause Press any key to select a %name file...
	
	REM Multiple file types separated by a ; require us to be in the desired directory.
	pushd "%@parent[%pattern]"
	select /1 /h set file=%_cwd\(%name)
	popd >& nul:
	set file=%@UnQuote[%file]
	
	iff %@files["%file"] != 1 then
		EchoErr A %name file was not selected.
		return 1
	endiff
	
endiff

set file=%@UnQuote[%file]
EndLocal /d file

return 0

:dir

set name=%@FileName[%pattern]
set parent=%@parent[%pattern]

iff not IsDir "%parent" then
	EchoErr The directory %parent does not exist.
	return 1
endiff

iff %@NumDirs["%pattern"] == 0 then
	EchoErr Unable to locate any %name directories in %parent.
	return 1
elseiff %@NumDirs["%pattern"] == 1 then
	set dir=%@FindFirst["%pattern%%@if[ %@IsWild[%pattern] == 0,\*,]",d]
else
	if defined prompt pause Press any key to select a %name directory...
	
	REM Multiple file types separated by a ; require us to be in the desired directory.
	pushd "%parent"
	select /a:d /h /1 set dir=("%pattern")
	popd
	
	iff "%dir" == "" then
		EchoErr A directory was not selected.
		return 1
	endiff
endiff

set dir=%@UnQuote[%dir]
EndLocal /d dir

return
