@echo off

REM Initialize
on break cancel 1
set program=%programs32\Iron Mountain\Connected BackupPC\Agent.exe
set BackupProgram=%programs32\Iron Mountain\Connected BackupPC\Backup.exe

REM Arguments
set command=start
set option=
set flag=

if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

iff %# gt 0 .and. IsLabel %1 then
	set command=%1
	shift
endiff

REM Run the command
gosub %command
quit %_?

:usage
text 1>&2
Conected Network Backup
usage: cnb [start|startup|backup|restore|service](start)
  service start|stop|restart|auto|demand|disable|enable|status [NoCheckHost] [host]
endtext
quit 2

:init
gosub IsInstalled & if %_? == 0 return 1
call cnb service start & if %? != 0 return 1
sleep 2
return 0

:backup
gosub init & if %_? != 0 return 1
echos Starting Connected Network Backup...
"%BackupProgram"
echo done.
return

:start
gosub init & if %_? != 0 return 1
start /pgm "%program"
pause
call cnb service stop
pskill Agent.exe
return %?

:startup
gosub IsInstalled & if %_? == 0 return 1
gosub IsRunning & if %_? == 1 return 1
call cnb service start
return %?

:close
gosub IsRunning & if %_? == 0 return 0
process.exe -q %@FileName[%program]
return %?

:IsInstalled
return %@if[IsFile "%program",1,0]

:IsRunning
gosub IsInstalled & if %_? == 0 return 0
return %@IsTaskRunning[%program]

:service

REM Arguments
set command=status
set host=
set NoCheckHost=

:ServiceGetArgs

iff %# gt 0 .and. IsLabel Service%1 then
	set command=%1
	shift & goto ServiceGetArgs
endiff

iff "%1" == "NoCheckHost" then
  set NoCheckHost=NoCheckHost
  shift & goto ServiceGetArgs
endiff

iff %# != 0 then
	set host=%1
	shift
endiff

if %# != 0 goto usage

REM Run the service command
gosub Service%command

return

:ServiceStart
:ServiceStop
:ServiceRestart
:ServiceAuto
:ServiceDemand
:ServiceDisable
:ServiceEnable
:ServiceStatus
:ServiceBriefStatus
call service %command wait %NoCheckHost AgentService %host
return

:restore
call chrome http://azscnbweb001.amr.corp.intel.com/ssws/faces/summary.jsp
return