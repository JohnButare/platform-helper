@echo off

REM Initialize
set SynergyDir=%programs32\Synergy

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=default
iff %# gt 0 then
  set command=%1
  shift
endiff

if not IsDir "%SynergyDir" quit 0
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 
synergy 
	default - load the default synergy profile for this host
	client <host> - start the synergy client and connect to host
	server <host> - start synergy server with the host profile
	end - close Synergy
endtext
quit 1

:default
switch %ComputerName
case oversoul
	call synergy server jjbutare-mobl
case jjbutare-mobl
	call synergy client oversoul
default
	echo No default synergy configuration for %ComputerName.
	quit 1
endswitch
return

:server

set ConfigFile=%UserDocuments\data\synergy\%ComputerName%_%1_%command.sgc

iff not exist "%ConfigFile" then
	echo Synergy server configuration does not exist for %1.
	quit 1
endiff

iff not %@IsTaskRunning[synergys] then
	start /pgm "%SynergyDir\synergys.exe" --config "%ConfigFile%" --debug WARNING --restart
endiff

return

:client

iff not %@IsTaskRunning[synergyc] then
	start /pgm "%SynergyDir\synergyc.exe" --debug WARNING --daemon %1
endiff

return

:close
taskend /f synergy
return
