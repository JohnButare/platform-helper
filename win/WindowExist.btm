@echo off
SetLocal
on break cancel 1

REM Arguments
iff %@IsHelpArg[%@UnQuote[%1]] == 1 then
  echo WindowExist [title] - Outputs 1 if the window title exists, otherwise outputs 0.
  echo   Title can contain wildcard characters (* or ?).  
	echo   WindowExist is called from the @WindowExist function.
  quit 1
endiff

REM If the UpdateTitle option is on (option //UpdateTitle=Yes), then tcc will update 
REM the title of the window with the command being run, including parameters.  This can 
REM cause @WinExeName to return the command prompt if the title is passed with a leading wildcard
REM as the last argument to a batch file.  This is why we must run WinExeName from a batch file
REM and change the tcc widnow title temporarily.
title WindowExist

REM Check for the window

REM Use WhichWin if the SysUtils plugin is loaded
iff IsPlugin WhichWin then

	REM Convert from tcc style wildcards (? and *) to regular egrep style (.* and .?)
	set title=%$
	set title=%@replace[*,.*,%title]
	set title=%@replace[?,.?,%title]
	
	set check=%@ExecStr[WhichWin /f t | egrep -i "^^%@UnQuote[%title]$"]
	
else
	REM WinExeName is faster, but does not find the title if it contains leading wildcards and requires elevated permissions ( outputs an error that cannot be filtered)
	set check=%@WinExeName[%$]
	
endiff

echo %@if["%check" == "",0,1]

REM Restore the window title
title TC

quit 0
