@echo off

REM Initialize

REM Get arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=console
iff %# gt 0 then
	set command=%1
	shift
endiff
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 1>&2
wmi [console|fix|log](console)
endtext
quit 1

:console
start /pgm wmimgmt.msc
return %?

:log
cdd "%WinDir\system32\wbem\Logs"
return 0

:fix

call ask `Are you sure you want to delete and repair the WMI repository?` n
if %? == 0 return 1
pause

cdd c:\temp
if not IsDir "%WinDir%\system32\wbem" gosub install
cdd "%windir%\system32\wbem"
net stop winmgmt
winmgmt /kill

if exist Rep_bak rd Rep_bak /s /q
rename Repository Rep_bak

echo.
echo Registering COM objects...
for %%i in (*.dll) (
	echos Registering %i...
	RegSvr32 -s %%i
	echo done
)

echo.
echo Registering services...
for %%i in (*.exe) (
	iff ("%i" != "wbemcntl.exe" .and. "%i" != "wbemtest.exe" .and. "%i" != "mofcomp.exe" ) then
		%i /RegServer
	endiff
)

echo.
echo Compiling...
for %%i in (*.mof,*.mfl) Mofcomp %%i

net start winmgmt

return 0

:install
if "%@search[wmicore.exe]" == "" return 1
wmicore /s
net start winmgmt
return 0
