@echo off
SetLocal 

REM Initialize
set args=
set title=*Notepad++
set program=%programs32\Notepad++\notepad++.exe

REM Initialize profile
set ProfileApp=NotepadPP
set ProfileMethod=%AppData\Notepad++

REM Configuration files
set ProfileFiles=config.xml contextMenu.xml shortcuts.xml stylers.xml userDefineLang.xml

REM Plugin configuration files
set ProfileFiles=%ProfileFiles *.ini plugins\config\*.ini

REM Argument
set command=start

if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

:GetArgs

iff %# gt 0 .and. IsLabel %1 then
	set command=%1
	shift & goto GetArgs	
endiff

iff "%1" == "/d" .or. "%1" == "/dup" .or. "%1" == "/duplicates" then
	set args=%args -multiInst 
	shift & goto GetArgs
endiff

REM Run command
gosub %command
quit %_?

:usage
text 1>&2
Notepad++ Text Editor
NotepadPP [start|close|profile|RestoreLangs](start) [/dup]
  profile dir|SaveDir|backup|restore [<profile name>|default](latest)
  RestoreLangs - restore langa.xml if it is corrupt
endtext
quit 1

:profile
call profile.btm %$
if %? == 1 goto usage
EndLocal /d
return %?

:start

iff %# == 0 then
	start /pgm "%program" %args
	return 0
endiff

REM For each file passed, exapnd wilddcards  and directory aliases
set files=
for file in (%$) (

	set ExpandedFile=%@expand[%@quote[%file]]

	REM search for the file - expands directory aliases
	iff "%ExpandedFile" == "" then
		set ExpandedFile=%@search[%@quote[%file]]
	endiff
	
	iff "%ExpandedFile" == "" then
		echo %file does not exist.
	else
		set files=%files %@quote[%ExpandedFile]
	endiff
)

if "%files" == "" return 1

start /pgm "%program" %args %files
return 0

:close
call task CloseKill title "%title" "%program"
return

:IsInstalled
return %@if[IsFile "%program",1,0]

:RestoreLangs
call sudo.btm copy "%PublicData\setup\langs.xml" "%programs32\Notepad++"
return