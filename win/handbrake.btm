@echo off
SetLocal

REM Initialize
set program=%programs32\Handbrake\Handbrake.exe
set presets=%AppData\HandBrake\presets.xml

REM Initialize profile
set ProfileApp=HandBrake
set ProfileMethod=%AppData\HandBrake
set ProfileFiles=*.xml

REM HandBrake profiles - copied from the preset in %UserData\HandBrake\user_presets.xml
set DLNA_profile=-O -e x264 -q 20.0 -a 1 -E faac -B 160 -6 auto -R Auto -D 0.0 -f mp4 -X 960 --loose-anamorphic -x cabac=0:ref=2:me=umh:b-adapt=2:weightb=0:trellis=0:weightp=0

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=cli
iff %# gt 0 .and. IsLabel %1 then
  set command=%1
  shift
endiff

REM Run command
gosub %command
quit %_?

:usage
text
usage: program [start|gui|cli|iOS](cli)
  convert [<profile>](AppleTV) <file> [<dest>] - convert a file using a profile 
  profile dir|SaveDir|backup|restore [profile](latest)
endtext
quit 2

:profile
call profile.btm %$
if %? == 1 goto usage
return %?

:start
:gui
if not IsFile "%program" return
start /pgm "%program"
return

:cli

iff %# == 0 then
	HandBrakeCLI.exe --help
else
	HandBrakeCLI.exe %$
endiff

return

:IsInstalled
return %@if[IsFile "%program",1,0]

:IsRunning

gosub IsInstalled
if %_? == 0 return 0

return %@IsTaskRunning[%program]

:convert

REM Arguments

REM get profile - DLNA profile is based on AppleTV profile with optimizations for other DLNA devices:
REM - no AC3 audio track, web optimized for streaming, no large file size
set profile=DLNA
iff not IsFile %@quote[%1] then
	set profile=%@UnQuote[%1]
	shift
endiff

REM Validate profile
iff defined %profile%_profile then
	set options=%@EvalVar[%profile%_profile]
else

	REM Validate the profile is defined
	HandBrakecli -z | egrep -i "+ %profile:" >& nul:
	iff %? != 0 then
		EchoErr Profile %profile is not defined.
		return 1
	endiff
	
	set options=--preset "%profile"
endiff

REM get src file name
if not IsFile "%1" goto usage
set src=%@full[%@UnQuote[%1]]
shift

REM get dest file name
set dest=%@path["%src"]%@name[%src].mp4
iff %# gt 0  then
	set dest=%@UnQuote[%1]
	shift
endiff

if %# != 0 goto usage

iff IsFile "%dest" then
	call ask `Overwrite destination file?` n
	if %? == 0 return 0
endiff

REM Handbrake requires the destination directory exist
set DestPath=%@path[%dest]
iff "%DestPath" != "" .and. not IsDir "%DestPath" then
	call MakeDir "%DestPath"
	if %? != 0 return %?
endiff

REM Convert the video
HandBrakeCLI.exe %options -v 1 --input "%src" -o "%dest"
return %@if[ %? == 0 .and. IsFile "%dest" ,0,1]

