@echo off
SetLocal

REM Initialize
set prefix32=%programs32\K-Lite Codec Pack\ffdshow
set prefix64=%programs64\KLCP64\ffdshow

REM Initialize arguments
set command=configure
set bits=32

REM Get arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

:GetArgs

iff IsLabel %1 then
	set command=%1
	shift
	goto GetArgs
endiff

iff "%1" == "32" .or. "%1" == "64" then
	set bits=%1
	shift & goto GetArgs
endiff

REM Get the specified program
set prefix=%@EvalVar[prefix%bits]
set program=%prefix\ffdshow.ax

REM Run command
if not IsLabel %command goto usage
gosub %command
quit %_?

:usage
text 1>&2
usage: FFDShow [32|64](32) [configure](configure)
  configure [audio|video|vfw](video)
endtext
quit 1

:IsInstalled
return %@if[IsFile "%program",1,0]

:configure

set command=ConfigureVideo
iff %# gt 0 then
	set command=Configure%1
	shift
endiff
if not IsLabel %command goto usage

gosub %command
return %_?

:ConfigureVideo
if IsFile "%program" rundll32 "%program",configure
return

:ConfigureAudio
if IsFile "%program" rundll32 "%program",configureAudio
return

:ConfigureVfw

set dir=%WinDir\system32

REM If we are on x64 and want to configure 32 bit Video For Windows, set dir to the x86 system32
if %@OsArchitecture[] == x64 .and. %bits == 32 set dir=%WinDir\SysWow64

REM Open the configuration screen
iff IsFile "%dir\ff_vfw.dll" then
	rundll32 "%dir\ff_vfw.dll",configureVFW
endiff

return
