@echo off
setlocal

:Variable
Iff IsDir x:\Data\DiskCatalog\. Then
	set dir=x:\Data\DiskCatalog
Else
	set dir=c:\Data\DiskCatalog
EndIff
if not isdir "%dir" md "%dir"

:Arguments
iff %# gt 0 then
	iff %1 == all .or. %1 == create .or. %1 == createf .or. %1 == createl .or. %1 == find .or. %1 == update .or. %1 == createfa then
		set action=%1
		shift
	else
		set action=show
	endiff
elseiff %# == 0 then
	set action=all
endiff

REM createf: Create with list of files in root direcotry
REM createl: Create with volume label of CD only
iff "%action" == "create" .or. "%action" == "createf" .or. "%action" == "createl" .or. "%action" == "createfa" then
	REM Get catalog drive
	if %# lt 1 goto usage
	set CatDrive=%1
	if not isdir %CatDrive goto usage
	shift
	if %# != 0 goto usage
	gosub CheckDriveReady   
	
	REM Get label
	set label=%@label[%CatDrive]
	
	REM Remove label trailing letter B or B.
	set lastChar=%@right[1,%label]
	iff %lastChar == a .or. %lastChar == b then
		set label=%@substr[%label,0,%@eval[%@len[%label]-1]]	
	endiff
	
	REM Set label case
	set label=%@lower[%label]
	set label=%@upper[%@left[1,%label]]%@substr[%label,1]
	
	set file=%dir\%label.txt
	REM ,m option does not calculate MB correctly, so must do it manually.
  set FreeSpace=%@eval[%@diskfree[%CatDrive] / 1024 / 1024=0.0]
	set UsedSpace=%@eval[%@diskused[%CatDrive] / 1024 / 1024=0.0]
	set TotalSpace=%@eval[%@disktotal[%CatDrive] / 1024 / 1024=0.0]
	echo *********************************************************** > "%file"
	echo * %label (%FreeSpace%MB Free / %UsedSpace%MB Used) >> "%file"
	echo *********************************************************** >> "%file"
	iff "%action" == "createf" then
		dir /1 /b %CatDrive >> "%file"
	else iff "%action" == "create" then
		tree /a %CatDrive\ >> "%file"
	else iff "%action" == "createfa" then
		dir /1 /b /s %CatDrive >> "%file"
	endiff
	echo CREATED: %file
	gosub CreateCatalog
	
elseiff "%action" == "update" then
	gosub CreateCatalog
	
elseiff "%action" == "find" then 
  REM Get application text 
  if %# lt 1 goto usage
  set App=%1
  shift
  if %# != 0 goto usage

  cdd %dir
  egrep -i "\* |%App" all.txt

elseiff "%action" == "show" then 
	iff isdir %1 then
		set CatDrive=%1
		gosub CheckDriveReady
		set file=%dir\%label.txt
		if not exist "%file"  call DiskCatalog create %1
	elseiff %# == 1 then
		set file=%dir\%1.txt
		if not exist "%file" goto usage
	else
		goto usage
	endiff
	e "%file"

elseiff "%action" == "all" then
	"%dir\all.txt"
else
	goto usage
endiff
goto Done

REM CreateCatalog
:CreateCatalog
echos Creating catalog...
echos usage report...
pushd %dir
del /q /e all.txt %temp\usage.txt
egrep "MB Used" *.txt > %temp\usage.txt

REM Update catalog
pushd %dir
echo *********************************************************** > all.txt
echo * Catalog Index >> all.txt
echo *********************************************************** >> all.txt
for %line in (@%temp\usage.txt) do (echo %@word[":",1,%line] >> all.txt)
echos >> all.txt
del /q /e %temp\usage.txt
echos contents...
for file in (*.txt) if %file != all.txt (echo `` >> all.txt && cat "%file" >> all.txt)

echo done
return

:CheckDriveReady
iff %@ready[%CatDrive] == 0 then
	echo ERROR: Drive %CatDrive is not ready.
	quit 1
endiff
return

:Done
quit 0

:Usage
echo `USAGE: %0 create <drive> -- Create catalog`
echo `                   createf <drive> -- Catalog of files in root`
echo `                   createfa <drive> -- Catalog of files`
echo `                   createl <drive> -- Catalog of label only`
echo `                   update -- Update Catalog`
echo `                   show [<label>|<drive>] -- Show catalog`
echo `                   find <application> -- Find application in catalog`
echo `                   [<label>|<drive>] -- Show catalog`
echo `                   all(default) -- Show all catalogs`
quit 1
