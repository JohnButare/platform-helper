@echo off
REM echo ProgramFiles=%ProgramFiles
REM echo system32=%system32
REM pause
REM quit

REM Initialize
set TcStartDir=%@left[-1,%@path[%_batchname]]

REM Define operating system directories
call "%TcStartDir\os.btm" FindDirsInit

REM Determine if we need to perform one time initialization
set force=%@if[ "%1" == "force" .or. "%1" == "/force" ,true,]
set full=%@if[ "%@alias[PublicAlias]" == "" .or. "%@alias[UserAlias]" == "" .or. ^
	defined force ,true,]

REM Perform one time initialization
iff defined full then

  REM Run sharalias to allow global settings (aliases and functions) to be saved when TakeCommand is not running
  shralias >& nul:

  REM Initialize Take Command
  iff exist "%TcStartDir\TcInit.btm" then
    call "%TcStartDir\TcInit.btm" %$
  endiff

endiff

REM Update path if started from a portable device 
iff IsDir "%TcStartDir\UserBin" then
  set UserBin=%TcStartDir\UserBin
  set path=%TcStartDir;%TcStartDir\%@OsArchitecture[];%UserBin;%path
endiff

REM Remaining startup for permanent shells.  Transient shells are started in a pipe (|) or with tcc /c.
if %_TRANSIENT == 1 quit 0

REM Get the AdminIndicator
echo %@IsElevated[] >& nul:

set TitlePrompt=tc%AdminIndicator
title %TitlePrompt

REM Set common executable extensions
iff IsFile "%TcStartDir\extension.btm" then
	call "%TcStartDir\extension.btm"
endiff

REM Default prompt
prompt ^
	%@lower[%ComputerName]%AdminIndicator ^
	`%@UnQuote[%@if[ %@len[%@lfn[%_cwd]] gt 20,%@quote[%@drive[%_cwd]:%@FileName[%@lfn[%_cwd]]],%@quote[%@lfn[%_cwd]]]]>`

REM User startup
iff "%UserProfile%" != "" .and. IsFile "%UserBin\TcStart.btm" then
  call "%UserBin\TcStart.btm" %$
endiff

REM If we are not re-initializing clear the screen
iff not defined force then
	cd
	cls
endiff

REM Cleanup
unset force