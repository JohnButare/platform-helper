@echo off

REM Initialize
set IsoFile=Data\Install\Microsoft\XP\setup\en_windows_xp_professional_n_with_service_pack_3_x86_cd_vl_x14-78118.iso

REM Arguments
iff %# == 0 then
  set 1=cd
elseiff %# != 1 then
  goto usage
endiff
iff not IsLabel %1 goto usage

gosub %1
goto done

:mount
gosub FindIso
call iso mount 1 "%file"
return

:FindIso
call FindPublicDoc "%IsoFile"
if %? == 1 quit %?
return

REM Instructions in Windows Operating Systrm.doc, reference http://www.helpwithwindows.com/WindowsXP/winxp-sp2-bootcd.html
:slipstream
gosub SlipstreamInit
gosub SlipstreamRecreate
gosub SlipstreamIntegrate
gosub SlipstreamFinalize
return

:SlipstreamInit

iff %ComputerName != oversoul then
  echo ERROR: Slipstream must be created on Oversoul.
  quit 1
endiff

set xp=%AllUsersProfile\Documents\data\install\Microsoft\XP
set setup=%xp\setup
set slip=%setup\slipstream
set kb=%xp\update

return

REM Mount the original XP CD and copy it
:SlipstreamRecreate

gosub SlipstreamInit

call ask `Recreate slipstream image?` n
iff %? == 1 then
  echo Please wait...removing old XP cd.
  rm -fr "%slip"
endiff

iff not IsDir "%slip" then
  iso "%setup\original.iso" 1
  rcd "c:\dev\iso1\." "%slip"
endiff

return

REM Integrate OS updates to the CD image
:SlipstreamIntegrate

gosub SlipstreamInit

REM Updates
gosub integrate "%xp\sp2\WindowsXP-KB835935-SP2-ENU.exe"
gosub integrate "%kb\WindowsInstaller-KB893803-v2-x86.exe"
gosub integrate "%kb\WindowsXP-KB898461-x86-ENU.exe" REM Permanent Package Installer 
gosub integrate "%kb\WindowsXP-KB905915-x86-ENU.exe" REM Internet Explorer 
gosub integrate "%kb\Windows-KB909520-v1.000-x86-ENU.exe" REM Microsoft Base Smart Card Cryptographic Service Provider Package
gosub integrate "%kb\WindowsMedia-KB891122-x86-ENU.exe" REM Update for WMDRM-enabled Media Players
gosub integrate "%kb\WindowsXP-KB887472-x86-enu" REM Windows Messenger

REM Security Update for Windows XP
gosub IntegrateList "KB896358 KB890859 KB901214 KB890046 KB896422 KB896428 KB885250 KB896423 KB913446 KB911927 KB908519 KB912919 KB896424 KB905749 KB900725 KB902400 KB899589 KB901017 KB905414 KB893756 KB899591 KB899587 KB888302 KB891781 KB888113 KB873339 KB885836 KB886185"
gosub integrate "%kb\WindowsXP-KB904706-v2-x86-ENU.exe"

REM Update for Windows XP
gosub IntegrateList "KB910437 KB894391 KB887742 KB900930"
gosub integrate "%kb\WindowsXP-KB896344-v2-x86-ENU.exe"

REM Finalize the slipstream CD image
:SlipstreamFinalize

gosub SlipstreamInit

REM Copy the boot image file
copy /q "%setup\config\Microsoft Corporation.img" "%slip"

REM Set unattended options
copy /q "%setup\config\nLiteSlipstream.ini" "%programs32\nLite\presets\slipstream.ini"
"%programs32\nLite\nLite.exe"

REM Burn the slipstream files to an ISO image
ShellRun "%setup\config\slipstream.nrb"
start explorer "%slip"
echo - Drag slipstream files to Nero compilation
echo - Burn to %setup\slipstream.iso

return

REM Integrate a list of OS updates to the CD image
:IntegrateList [list]
for %kbnum in (%@UnQuote[%list]) gosub integrate "%kb\WindowsXP-%kbnum%-x86-enu.exe"
return

REM Integrate an OS update to the CD image
:integrate [file]
set file=%@quote[%file]

iff exist %file then
  echo Integrating %@FileName[%file]...
  %file /integrate:"%slip"
else
  echo WARNING: Update %@FileName[%file] does not exist.
  pause
endiff

:done
quit 0

:usage
text
USAGE: xp [cd|mount|install|RestoreInf](cd)
  slipstream|SlipstreamRecreate|SlipstreamIntegrate|SlipstreamFinalize - XP slipstream commands
  cd - change to the installation folder
  mount - mount the slipstream installation CD iso image
  install - install on a virtual machine
  slipstream - prepare the slipstream image
  CleanupPrograms - cleanup programs
  CleanRecent - cleanup OS and Office recent documents 
  RestoreInf - restore Windows XP INF files
endtext
quit 1

:cd
gosub FindIso
cdd "%@path[%file]"
return

:install
gosub mount
text
- (if an existing OS is present) ESC for Boot Menu, select CD-ROM Drive), space
- Format the partition using the NTFS file system (Quick)
- Help protect your PC, select Not right now (Automatic Updates will be enabled later)
- Will this computer connect to the Internet directly, or through a network? 
  Yes, this computer will connect through a local area network or home network.
- Ready to register with Microsoft? No, not at this time
- Your name=<network login id>
- VMware, VM, Install VMware Tools, reboot
- Run "\\192.168.1.2\Public\Documents\data\bin\tcc" "\\192.168.1.2\Public\Documents\data\bin\install.btm" OsCore
endtext

return

:RestoreInf

iff "%_WinVer" != "5.1" then
  echo ERROR: inf files can only be restore on Windows XP.
  quit 1
endiff

iff exist "%WinDir\inf\iis.inf" then
  quit 0
endiff

set file=data\install\Microsoft\XP\Volume License\XpInf.zip
call FindPublicDoc "%file"

unzip.exe -Un "%file" -d "%WinDir"

return
