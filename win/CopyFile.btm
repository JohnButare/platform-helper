@echo off

REM Initialize
on break cancel 1

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set different=
set SizeDifferent=

:GetArgs

iff "%1" == "/different" then
	set different=true
	shift
	goto GetArgs
endiff

iff "%1" == "/SizeDifferent" then
	set SizeDifferent=true
	shift
	goto GetArgs
endiff

if %# lt 2 goto usage
set src=%@UnQuote[%1]
set dest=%@UnQuote[%2]

set CopyOptions=/q
iff %# != 0 then
	set CopyOptions=%3$
endiff

if not exist "%src" quit 0

REM If src is a file (no wildcards) check for differences
iff IsFile "%src" then
	
	iff IsDir "%dest" then
		set dest=%dest\%@FileName[%src]
	endiff
	
	iff IsFile "%dest" then
		if defined different .and. %@FileAge["%src",w] == %@FileAge["%dest",w] quit 0	
		if defined SizeDifferent .and. %@FileSize["%src"] == %@FileSize["%dest"] quit 0	
	endiff
	
endiff

copy %CopyOptions "%src" "%dest"
quit %?

:usage
text 1>&2
usage: CopyFile [/different] [/SizeDifferent] <src> <dest> <options>
  /different - copy if the destination last modification time is different
  /SizeDifferent - copy if the destination size is different
endtext
quit 1
