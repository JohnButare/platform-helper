@echo off
SetLocal

REM Initialize
set OversoulProfiles=default center living game
set profiles=%@EvalVar[%ComputerName%Profiles]

set ConfigDelay=1

call MakeDir.btm %UserData\Monitor
set CurrentProfileFile=%UserData\other\Monitor Profile.txt
set CurrentProfile=
if IsFile "%CurrentProfileFile" set CurrentProfile=%@line["%CurrentProfileFile",0]

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=config
iff IsLabel %1 then
	set command=%1
	shift
endiff
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 1>&2
usage: MonitorConfigure [config|info|toggle](config)
  config [/force] [<profile>](<first>) - configure monitors for the specified profile
  info - list the monitor profiles for this computer
  toggle - toggle between the profiles for this computer
endtext
quit 1

:info

echo Profiles: %profiles
echo  Current: %@if[ defined CurrentProfile ,%CurrentProfile,unknown]

return

:toggle

set NumProfiles=%@words[%profiles]
set NewProfile=%@word[0,%profiles]

iff defined CurrentProfile then

	for /l %i in (1,1,%NumProfiles) (
		iff "%CurrentProfile" == "%@word[%@eval[%i-1],%profiles]" then
			iff %i != %NumProfiles then
				set NewProfile=%@word[%i,%profiles]
			endiff
		endiff
	)

endiff

call MonitorConfigure config %NewProfile

return

:config

REM Arguments

set force=
iff "%1" == "/force" .or. "%1" == "/f" then
	set force=true
	shift
endiff

iff "%profiles" == "" then
	echo %ComputerName does not have any monitor profiles configured.
	return 1
endiff

set NewProfile=%@word[0,%profiles]
iff %# gt 0 .and. %@IsInList[%1,%profiles] then
  set NewProfile=%1
  shift
endiff

if %# != 0 goto usage

iff not defined force .and. "%CurrentProfile" == "%NewProfile" then
	echo The %NewProfile monitor confiugration has already been configured.
	return 0
endiff

echo Configuring the %NewProfile monitor configuration...

iff IsLabel %ComputerName%_%CurrentProfile%_off then 
	gosub %ComputerName%_%CurrentProfile%_off
	if %_? != 0 return %_?
endiff

call UltraMon.btm load %NewProfile
if %_? != 0 return %_?

call PowerMixer.btm load %NewProfile
if %_? != 0 return %_?

iff IsLabel %ComputerName%_%NewProfile%_on then 
	gosub %ComputerName%_%NewProfile%_on
	if %_? != 0 return %_?
endiff

REM Record the new monitor profile
echo %NewProfile > "%CurrentProfileFile"

return 0

REM Profile commands:
REM Turn NumLock on or off (keyboards without separate number entry): call SetKeyState NumLock on|off
REM Center the mouse on the primary monitor: call mouse.btm center
REM Load an audio profile: call PowerMixer load <profile>
REM RealTek audio control panel: call RealTekAudio.btm start

REM 
REM
REM Oversoul
REM 

:oversoul_default_on

echo - Connect the study left monitor
pause

call ask.btm `Start sidebar (if the left monitor is displayed correctly)?` n
if %? == 1 call SideBar.btm 32 start

return 0

:oversoul_default_off

call SideBar close

echo - Turn on and connect the %NewProfile monitor
if "%NewProfile" == "game" echo - Turn off the study speakers and turn on the game room speakers
pause

return 0

:oversoul_living_on
return 0

:oversoul_living_off
return 0

:oversoul_game_on
return 0

:oversoul_game_off
return 0