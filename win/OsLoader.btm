@echo off
SetLocal

REM Initialize
set program=%programs32\OS Loader 2000\setup.exe

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=
iff IsLabel %1 then
	set command=%1
	shift
endiff
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 1>&2
usage: OsLoader boot|setup|register
  boot <n>|win32|win64 - boot the Operating System on partition n
    0=floppy, 1=OS on partiton 1, etc.
endtext
quit 1

:setup

gosub CheckInstalled
cd "%@path[%program]"

start /pgm setup.exe

return

:register

gosub CheckInstalled
cd "%@path[%program]"

REM Restore the KeyFile
copy /q KeyFile.bak KeyFile.dat

REM Register
start /pgm setup.exe /r:11052008-41559-6081

return

:IsInstalled
return %@if[IsFile "%program",1,0]

:CheckInstalled
if not IsFile "%program" quit 1
return

:boot

gosub CheckInstalled

REM Arguments

set CheckOs=
iff "%1" == "CheckOs" then
	set CheckOs=true
	shift
endiff

if %# != 1 goto usage
set os=%1
shift

REM Translate os name to a number
iff "%ComputerName" == "Oversoul" then
	switch %os
	case win64
		set os=1
	case win32
		set os=2
	EndSwitch
endiff

iff %@numeric[%os] == 0 .or. "%os" != "%@int[%os]" then
	if not defined CheckOs echo %os is not a valid Operating System.
	return 1
endiff

if defined CheckOs return 0

REM OS Loader setup requires it is run fromt he current directory
cd "%@path[%program]"

echo Issuing reboot to OS %os...
start /pgm setup.exe /b:%@eval[%os + 1]

return 0
