@echo off

REM Initialize

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

if %# != 1 goto usage
set file=%@UnQuote[%1]
shift

if %@IsElevated[] == 0 return 2

set process=%@ExecStr[handle -u "%file" | egrep -i pid]
set result=%?

if %result == 5 quit 2
if %result == 1 .or. "%process" == "" quit 1

REM Since the process executable can contain spaces, use a regular expression to search for the name before the pid
set ProcessName=%@trim[%@RegExSub[0,^.*(?=pid:),%process]]

REM The PID is in the word after pid:
set ProcessPid=%@word[0,%@RegExSub[0,(?<=pid:).*,%process]]

REM The user is after the pid: and before the next :
set ProcessUser=%@RegExSub[0,(?<=:)(.)*:,%process]
set ProcessUser=%@word[":",0,%ProcessUser]
set ProcessUser=%@word[1-%@eval[%@words[%ProcessUser]-2],%ProcessUser]

set HandleId=%@word[":",1,%process]
set HandleId=%@word[-0,%HandleId]

echo %ProcessName-%ProcessPid-%ProcessUser-%HandleId
quit 0

:usage
text 
GetHandleInfo <name>: Get information about the specified handle.  Exit codes:
  0: the handle information is retrieved and sets ProcessPid, ProcessUser, and HandleId
	1: the handle could not be found.
  2: the user does not have sufficient privileges (administrators group running with elevated privileges).
endtext
quit 1

