@echo off
SetLocal
REM Move files or directories from to another directory.   
REM - internal move command with wildcards does not find directories
REM - mv.exe does not work with files or directories that begin with a period.

REM Arguments
set quiet=
set dest=
set src=

:GetArgs
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

iff "%1" == "/quiet" .or. "%1" == "/q" then
	set quiet=/q
	shift & goto GetArgs
endiff

iff "%src" == "" .and. %# gt 0 then
	set src=%@UnQuote[%1]
	shift & goto GetArgs
endiff

iff "%dest" == "" .and. %# gt 0 then
	set dest=%@UnQuote[%1]
	shift & goto GetArgs
endiff

if %# != 0 .or. "%src" == "" .or. "%dest" == "" goto usage

iff not exist "%src" then
	echo Source "%src" does not exist.
	quit 1
endiff

iff %@IsWild[%src] == 1 then
	for /a: /h file in ("%src") (
		move %quiet %@quote[%file] "%dest"
		if %? != 0 return %?
	)
else
	move %quiet "%src" "%dest"
	quit %?
endiff

quit 0

:usage
text 1>&2
usage: MoveEx src dest
endtext
quit 1
