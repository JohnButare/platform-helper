@echo off
setlocal

REM Initialize
set stamp=%@DateStamp[]

REM Initialize arguments
set dest=
set local=
set move=
set quiet=

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

:GetArgs

iff "%1" == "/dest" then
	set dest=%@UnQuote[%2]
	shift 2 & goto GetArgs
endiff

iff "%1" == "/quiet" then
	set quiet=/silent
	shift & goto GetArgs
endiff

iff %1 == move then
  set move=true
  shift & goto GetArgs
endiff

iff %1 == local then
  set local=true
  shift & goto GetArgs
endiff

if %# lt 1 goto usage

REM Loop over each pattern.
for %file in (%$) gosub BakFile
goto done

:BakFile

set file="%@unquote[%file]"
set dir=%@path["%@full[%file]"]
set file=%@FileName[%file]

iff not exist "%dir\%file" then
  echo %dir%%file does not exist.
  quit 1
endiff

REM BakDirPrefix
iff defined local then
  set BakDirPrefix=%PublicData\bak
elseiff defined dest then
	set BakDirPrefix=%dest\bak
else
  set BakDirPrefix=%dir%bak
endiff

set BakDir=%BakDirPrefix
iff not IsDir "%BakDir" then
   mkdir "%BakDir"
   iff not IsDir "%BakDir" then
      EchoErr Unable to create %BakDir directory.
      quit 1
   endiff
endiff

set cnt=1
set BakDir=%BakDirPrefix\%stamp.%cnt
do while exist "%BakDir\%file"
  set cnt=%@eval[%cnt + 1]
  set BakDir=%BakDirPrefix\%stamp.%cnt
enddo

iff not IsDir "%BakDir" then
  mkdir "%BakDir"
  iff not IsDir "%BakDir" then
     EchoErr Cannot create %BakDir directory.
    quit 1
  EndIff
endiff

iff defined move then
  move /q "%dir%%file" "%BakDir"
  if not defined quiet echo Removed %file to %BakDir
else
  if not defined quiet echo Backing up %file to %BakDir
	iff IsDir "%dir%%file" then
		call CopyDir.btm %quiet "%dir%%file" "%BakDir\%file"  
	else
		copy /q "%dir%%file" "%BakDir"  
	endiff
endiff
return

:Usage
echo `bak [/dest <destination dir>] [/quiet] [move] [local] <pattern> [<pattern>]...`
quit 1

:Done
quit 0
