@echo off

REM Cleanup the a directory but preserve the decriptions.  mv must be used since native Take Command commands would remove the description.

iff %# != 1 then 
	echo USAGE: %0 dir
	quit 1
endiff

set dir=%@unquote[%1]

REM Check if the directory exists.
iff not isdir "%dir" then
	echo ERROR: %dir is not a directory.
	quit 1
endiff

REM Exit if there are no visible files in the directory.
iff %@files["%dir\*",-d-h] == 0 then
	quit 0
endiff

call ask `Do you want clean all files in %dir?` n
iff %? == 0 then 
	quit 0
endiff

REM Change to the directory since rm * only works from within a directory
pushd "%dir"

REM Move the desription file if it exists.
iff exist Descript.ion then
	mv Descript.ion ..
endiff

REM delete the files in the folder.
rm *

REM Move the desription file back if it exists.
iff exist ..\Descript.ion then
	mv ..\Descript.ion .
endiff

popd
