@echo off
SetLocal

REM Initialize
gosub initialize
if %_? != 0 quit %_?

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=
iff %# gt 0 then
  set command=%1
  shift
endiff

iff not IsLabel %command goto usage

if %# != 0 goto usage

REM Run command
gosub %command
set result=%_?

quit %result

:usage
text 
usage: office [CleanInstall|FixIcons|architecture|version|init]
  CleanInstall - Cleanup office installation files
endtext

quit 1

:CleanInstall

REM Only Office 2003 (Office11) can be cleaned with LisTool
if not exist "%programs32\Microsoft Office\OFFICE11\mso.dll" return 0

call word close
echo - Delete and disable LIS, check all Office programs
start /wait /pgm LISTOOL.EXE
call word startup

return

REM FixIcons - set legacy Office formats (i.e. doc, ppt) to the icons from the Office program. 
REM - The default icons are in c:\Windows\Installer.:
REM   - Office 14: C:\Windows\Installer\{20140000-0011-0000-1000-0000000FF1CE}
REM   - Office 12 C:\Windows\Installer\{90120000-0030-0000-0000-0000000FF1CE}
REM  - New office formats (docx, pptx, ...) 
REM     - Icons are rounded and specified in HKCR\Word.Document.12\DefaultIcon
REM  - Legacy office formats (doc, ppt, ...) 
REM     - icons are square and specified in HKCR\Word.Document.8\DefaultIcon
REM     - default legacy icons numbers are Word=1 Excel=28 PP=17

:FixIcons

iff "%OfficeArchitecture" == "x64" then
	EchoErr An icon repair file does not exist for x64 Office.  
	return 1
endiff

call ask,btm `Do you want to repair Office icons?` y
if %? == 1 call registry.btm import "%AllUsersProfile\documents\data\setup\Office x86 Icons for Windows %@OsArchitecture[].reg"

return 0

:initialize

set OfficeVersion=15
set OfficeTitle=* - 
set WordFastStartTitle=Word Fast Start* - Word
set OfficeArchitecture=x86
set OfficeBits=32
set Office365=

REM Office location
iff IsFile "%programs64\Microsoft Office 15\root\office15\WinWord.exe" then
	set OfficeDir=%programs64\Microsoft Office 15\root\office15
	set OfficeBits=64
	set Office365=true

elseiff IsFile "%programs32\Microsoft Office 15\root\office15\WinWord.exe" then
	set OfficeDir=%programs32\Microsoft Office 15\root\office15
	set Office365=true

elseiff IsFile "%programs64\Microsoft Office\Office15\WinWord.exe" then
	set OfficeDir=%programs64\Microsoft Office\Office15
	set OfficeBits=64

elseiff IsFile "%programs32\Microsoft Office\Office15\WinWord.exe" then
	set OfficeDir=%programs32\Microsoft Office\Office15
	set OfficeVersion=15

elseiff IsFile "%programs64\Microsoft Office\Office14\WinWord.exe" then
	set OfficeDir=%programs64\Microsoft Office\Office14
	set OfficeVersion=14
	set OfficeBits=64
	set OfficeTitle=* - Microsoft
	set WordFastStartTitle=Word Fast Start* - Microsoft Word
	
elseiff IsFile "%programs32\Microsoft Office\Office14\WinWord.exe" then
	set OfficeDir=%programs32\Microsoft Office\Office14
	set OfficeVersion=14
	set OfficeTitle=* - Microsoft
	set WordFastStartTitle=Word Fast Start* - Microsoft Word
	
elseiff IsFile "%programs32\Microsoft Office\Office12\WinWord.exe" then
	set OfficeDir=%programs32\Microsoft Office\Office12
	set OfficeVersion=11
	set OfficeTitle=* - Microsoft
	set WordFastStartTitle=Word Fast Start* - Microsoft Word
	
elseiff IsFile "%programs32\Microsoft Office\Office11\WinWord.exe" then
	set OfficeDir=%programs32\Microsoft Office\Office11
	set OfficeVersion=11
	set OfficeTitle=* - Microsoft
	set WordFastStartTitle=Word Fast Start* - Microsoft Word

else
	set OfficeDir=
	return 1
EndIff

REM x64 Office if we are running under x64 OS and Office is installs to %programs64
iff "%@OsArchitecture[]" == "x64" .and. %@index[%OfficeDir,%programs64\] == 0 then
	set OfficeArchitecture=x64
	set OfficeBits=64
endiff

return 0

:architecture
echo %OfficeArchitecture
return 0

:version
echo %OfficeVersion
return 0

:init
EndLocal OfficeDir OfficeVersion OfficeBits Office365 OfficeArchitecture OfficeTitle WordFastStartTitle
return 0
