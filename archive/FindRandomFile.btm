@echo off

if %# != 1 goto usage

set folder=%@unquote[%@trim[%1]]

iff %folder == next then

	iff "%RandomFileList" == "" .or. not exist "%RandomFileList" .or. "%RandomFileLines" == "" then
		EchoErr FindRandomFile must first be called with a folder.
		quit 1
	endiff
	
	set file=%@line[%RandomFileList,%@random[0,%RandomFileLines]]

elseiff %folder == done then
	gosub cleanup
	
else 

	set randomFileList=%@unique[%temp]
	
	iff not IsDir %@path[%folder] then
		echo ERROR: %@path[%folder] does not exist.
		quit 1
	endiff
	
	REM Run directory listing on the remote host, not over the wire (~10X faster)
	iff "%@left[2,%folder]" == "\\" then
		set host=%@word["\",0,%folder]
		psexec \\%host tcc.exe /c dir /s /b "%folder" > %randomFileList
	else
		dir /s /b "%folder" >& %randomFileList
	endiff
	if %? != 0 quit %?

	set RandomFileLines=%@lines[%randomFileList]

	iff %@lines[%RandomFileList] == -1 then
		EchoErr No files found for %folder.
		gosub cleanup
		quit 1
	endiff

endiff

quit 0

:usage
text 1>&2
usage: FindRandomFile <folder>|next|done
folder - create a list of files in or below folder, which can include a file specification, 
  such as "%AllUsersProfile\Documents\Pictures\*.jpg".  
next - set the file environment variable to a random file from the previous created list of files.
done - remove the list of files
endtext
quit 2

:cleanup
call DelFile.btm "%randomFileList"
return
