@echo off
SetLocal
on break cancel 1

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=startup
iff "%1" == "startup" .or. "%1" == "close" then
	set command=%1
	shift
endiff

if %# == 0 quit 0

set apps=%$
iff %@words[%apps] gt 1 then

	for %app in (%apps) (
		call app.btm %command %app		
	)
	quit 0
	
endiff

set app=%apps

REM Resolve app aliases
if "%app" == "tc" set app=TrueCrypt
if "%app" == "keys" set app=AutoHotKey
if "%app" == "PuttyAgent" set app=pu
if "%app" == "wmc" set app=WindowsMediaCenter
if "%app" == "wmp" set app=WindowsMediaPlayer
if "%app" == "X" .or. "%app" == "XWindows" set app=Xming

REM Check if the application is known
iff %@IsInstalled[%app] == 0 .and. not IsLabel %app then
	quit 1
endiff

REM Return if the app is already running or closed
iff %@IsRunning[%app] == %@if[ "%command" == "startup",1,0] then
	quit 1
endiff

REM Show status
iff %@IsInstalled[%app] == 1 then
	echo %@if[ "%command" == "startup",Starting,Closing] %app...
endiff

REM Run command
iff IsLabel %app then
	gosub %app
else
	call %app%.btm %command 
endiff
quit %_?

:usage
text 1>&2
usage: app [startup|close](startup) <application>
endtext
quit 1

:start [program TaskTitle arguments]

if "%program" == "" return 1

if not IsFile %@quote[%program] .and. "%@search[%program]" == "" return 1

iff "%@UnQuote[%TaskTitle]" != "" then
	set title=title %@quote[%TaskTitle]
else
	set title=
endiff

iff "%command" == "startup" then
	call task start %idle %title %@quote[%program] %@UnQuote[%arguments]
else
	call task close %title %@quote[%program]
endiff

return
:AnyDvd
if %@IsTaskRunning[AnyDVDtray] == 0 gosub start AnyDVD
return

:AspNetVersionSwitcher
if "%command" == "startup" gosub start "%programs\ASPNETVersionSwitcher\ASPNETVersionSwitcher.exe"
return

:CloneDvd
if "%command" == "startup" gosub start "%programs32\Elaborate Bytes\CloneDVD2\CloneDVD2.exe"
return

:Groove
gosub start "%programs32\Microsoft Office\Office12\GROOVE.EXE" "" -background
return

:IntelActiveMonitor
gosub start "%programs32\Intel\Intel(R) Active Monitor\iActvMon.exe"
return

:IntelDesktopControlCenter
set program=%programs32\Intel\Intel(R) Desktop Control Center\idcc.exe
if "%command" == "startup" .and. %@IsTaskRunning[idcc] == 0 .and. exist "%program" start /d"%@path[%program]" /pgm "%program"
return

:PinnacleGameProfiler

REM Pinnacle exceptions when started from the command prompt, so use ShellRun
set exe=%programs32\KALiNKOsoft\Pinnacle Game Profiler\pinnacle.exe
if IsFile "%exe" ShellRun "%exe"

return

:ProcessExplorer
:ProcExp
:pe
iff "%command" == "startup" then
	gosub start "%PublicDocuments\data\bin\%@OsArchitecture[]\ProcExp.exe" "Process Explorer*" "/t /p:l"
elseiff %@IsTaskRunning[procexp] == 1 then
	activate "Process Explorer*" restore >& nul:
	if %@IsWindowActive[Process Explorer*] == 1 keystack Alt-F x
endiff
return

:TakeCommand
if "%command" == "startup" call task start min %idle "tcmd.exe"
return

:cpu7icon
:gridy
:SpeedFan
:ThinkPadFanControl
:ZoomIt
:ShairPort4W

REM Initialize
set close=process -q
set program=%app.exe

REM Set program spefic defauls
switch "%app"
case "cpu7icon"
	set close=pskill
case "SpeedFan"
	set program=%programs32\SpeedFan\speedfan.exe
case "ThinkPadFanControl"
	set program=%programs\TPFanControl\TPFanControl.exe
	set close=pskill
endswitch

if "%program %@search[%@quote[%program]]" == "" return

REM Start or close the program
iff "%command" == "startup" then
	if %@IsTaskRunning[%program%] == 0 start /pgm %@quote[%program%]
else
	if %@IsTaskRunning[%program%] == 1 %close% %@FileName[%program]
endiff

return