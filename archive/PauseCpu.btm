@echo off

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set pauseDelayDummy=TIMEOUT

set cpu=25
set delay=60
set message=`Waiting %delay seconds for CPU to be less than %cpu%% utilized or press any key when ready`

iff %# == 3 then
  set cpu=%1
  set delay=%2
  set message=%3
elseiff %# == 2 then
  set cpu=%1
  set delay=%2
elseiff %# == 1 then
  set cpu=%1
elseiff %# != 0 then
  goto usage
endiff

REM Quit immediately (no messages) if the CPU is below the threshold
iff %_CpuUsage lt %cpu then
  quit 0
endiff

echos %message

do i = 1 to %delay

  set usage=%_CpuUsage
  echos .%usage
  iff %usage lt %cpu then
    echo.
    quit 0
  endiff
    
  inkey /x /w1 %%pauseDelayDummy
  
  iff %pauseDelayDummy != TIMEOUT then
    echo .(cancelled after %i seconds)
    quit 0
  endiff
  
  
enddo

echo .(delay period of %delay seconds has elapsed)

goto done

:usage
echo USAGE: %0 [cpu](25) [timeout](60) [message]
echo    cpu - wait until the CPU is below this utilization
echo    delay - maximum number of seconds to wait
echo    message - message to display
quit 1

:Done
quit 0
