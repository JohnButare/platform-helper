@echo off

REM Initialize
set ComKey=HKLM\SOFTWARE\Microsoft\Ole

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=start
iff %# gt 0 then
  set command=%1
  shift
endiff

iff not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 
USAGE: com [config|OleView|debug](config)
  debug [on|off|info](on) - log details COM events to the event log
endtext
quit 1

:debug

REM Arguments

set command=info
iff %# == 1 then
  set command=%1
  shift
endiff

if %# != 0 goto usage

if not IsLabel Debug%command goto usage

gosub Debug%command
return %_?

:DebugOn
echo COM debugging enabled.
call registry.btm set "%ComKey\ActivationFailureLoggingLevel" REG_DWORD 1
call registry.btm set "%ComKey\CallFailureLoggingLevel" REG_DWORD 1
return 0

:DebugOff
echo COM debugging disabled.
call registry.btm set "%ComKey\ActivationFailureLoggingLevel" REG_DWORD 0
call registry.btm set "%ComKey\CallFailureLoggingLevel" REG_DWORD 2
return 0

:DebugInfo

set ActivationFailureLoggingLevel=%@RegGet["%ComKey\ActivationFailureLoggingLevel"]
set CallFailureLoggingLevel=%@RegGet["%ComKey\CallFailureLoggingLevel"]

echo Debug=%@if[%ActivationFailureLoggingLevel == 1,true,false]
echo ActivationFailureLoggingLevel=%ActivationFailureLoggingLevel
echo CallFailureLoggingLevel=%CallFailureLoggingLevel

return 0

:config
if %# != 0 goto usage
start /pgm dcomcnfg
return

:OleView
if %# != 0 goto usage
start /pgm OleView
return
