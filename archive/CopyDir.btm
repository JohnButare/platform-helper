@echo off
SetLocal

REM Copy src directory to dest directory

REM Initialize
set rc=robocopy
iff "%RcOpts" == "" then
  set RcOptions=/ns /E /R:2 /W:2 /njh /njs 
else
	set RcOptions=%RcOpts
endiff
set CopyOptions=/g /s /u /h /j /k /z

REM Arguments
set arguments=
set mirror=
set quiet=
set silent=
set fast=
set test=
set time=

:GetArgs
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

iff "%1" == "/args" .or. "%1" == "/arguments" then
	set arguments=%2
	shift 2 & goto GetArgs
endiff

iff "%1" == "/m" .or. "%1" == "/mir" .or. "%1" == "/mirror" then
	set mirror=true
	set RcOptions=%RcOptions /mir
	shift & goto GetArgs
endiff

iff "%1" == "/q" .or. "%1" == "/quiet" then
	set quiet=true
	shift & goto GetArgs
endiff

iff "%1" == "/s" .or. "%1" == "/silent" then
	set silent=true
	set quiet=true
	shift & goto GetArgs
endiff

iff "%1" == "/f" .or. "%1" == "/fast" then
	set fast=true
	shift & goto GetArgs
endiff

iff "%1" == "/test" then
	set test=true
	set CopyOptions=%CopyOptions /n
	set RcOptions=%RcOptions /l /ndl
	shift & goto GetArgs
endiff

iff "%1" == "/t" .or. "%1" == "/time" then
	set timer=true
	shift & goto GetArgs
endiff

if %# != 2 goto usage
set src=%@UnQuote[%1]
set dest=%@UnQuote[%2]
shift 2

if "%src" == "" .or. "%dest" == "" goto usage

REM If src is a drive letter only, add a trailing \ so the root of the drive is copied.
if "%@right[1,%src]" == ":" set src=%src\.

REM RoboCopy errors if a \ is at the end
if "%@right[1,%src]" == "\" set src=%src.

if not IsDir "%src" quit 0

if not defined quiet echo Copying %src to %dest

REM timer
if defined timer timer on

REM Copy
iff defined fast .and. not defined mirror then
	REM Copy is faster than RoboCopy (~2x) but does not perform a verification, 4m31s vs 7m54s.
	copy %@if[ defined silent ,/q] %CopyOptions "%src" "%dest"
else

	REM Quote src only if it contains spaces.  RoboCopy errors if src has no spaces and it is quoted.
	if %@index[%src, ] != -1 set src="%src"
	
	%rc %@if[ defined silent ,/njh /njs /ns /nc /nfl /ndl /np] %src "%dest" %RcOptions
	
endiff

set result=%?

REM timer
if defined timer timer off

quit %result

:usage
text 1>&2
usage: CopyDir [/quiet] [/fast] [/time] [/mirror] [/test] <src> <dest>
  RcOpts - environment variable used for RoboCopy options if defined
  /mirror - make the src mirror the dest (deletes files from the dest not in src)
  /fast - use copy command if not mirroring, which is slightly faster than RoboCopy
  /quiet - do not display header
  /silent - do not display anything
endtext
quit 1
