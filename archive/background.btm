@echo off

REM Initialize
set program=%@search[bginfo.exe]

set WallpaperDir=%@PublicPictures[]\Wallpaper
set WallpaperInstallDir=\\oversoul\public\Pictures\Wallpaper
set ComputerProfile=%UserData\profile\default\bginfo %ComputerName Profile.bgi
set DefaultProfile=%UserData\profile\default\bginfo Profile.bgi

REM Get arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=update
iff IsLabel %1 then
	set command=%1
	shift
endiff
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text 1>&2
background install|update|UpdateSelected
  update [file](default) - update background using the specified configuration file,
    or the default profile in data\profile\default, named either 
    "bginfo <ComputerName> Profile.bgi" or "bginfo Profile.bgi"
  UpdateSelected - update the background for selected hosts (server, VM)
endtext
quit 1

:install

REM Libraries  only available on Windows 7 and up
if %_WinVer lt 6.1 return 0

REM Copy the default wallpaper images from the wallpaper installation directory
iff not IsDir "%WallpaperDir" then

	iff not IsDir "%WallpaperInstallDir" then
		EchoErr Wallpaper installation directory could not be located.
		return 1
	endiff
		
	call CopyDir.btm "%WallpaperInstallDir" "%WallpaperDir"
	if %? != 0 return %?
	
endiff

REM Create library for wallpaper
echo %@ClipW[%WallpaperDir] >& nul:
text
- Libraries, New Library, Wallpaper, Include all folders from <paste>
- right click on navigation pane, uncheck Show all folders
- Wallpaper library, Properties
  - Select Personal, Set save location
  - Optimize this library for=Pictures
  - Uncheck Show in navigation pane
- right click on navigation pane, check Show all folders
endtext
call explorer.btm 
pause

REM Screen Saver and desktop background
text
- Desktop Background, Browse..., Libraries, Wallpaper
- Screen Saver, Screen saver=Blank or Bubbles, Wait=10 minutes, check On resume display logon screen
- Save theme, Theme name=default
endtext
call os.btm appearance
pause

REM Set Logon screen background
iff %@IsElevated[] == 1 then

	call registry.btm set "HKLM\Software\Microsoft\Windows\CurrentVersion\Authentication\LogonUI\Background\OEMBackground" REG_DWORD 1
	call registry.btm set "HKLM\SOFTWARE\Policies\Microsoft\Windows\System\UseOEMBackground" REG_DWORD 1
	
	echo Click and paste to select the logon screen background...
	call explorer.btm "%WallpaperDir"
	start /pgm LogonChanger.exe
	pause
endiff

return

:update

REM Arguments
set file=
iff %# gt 0 then
	set file=%1
	shift
endiff

iff %# != 0 goto usage

REM Find default configuration file
iff "%file" == "" then
	iff exist "%ComputerProfile" then
		set file=%ComputerProfile
	elseiff exist "%DefaultProfile" then
		set file=%DefaultProfile
	endiff
endiff

REM Verify the configuration file exist
iff "%file" != "" .and. not IsFile "%file" then
	echo The profile does not exist.
	quit 1
endiff

start /pgm bginfo.exe %@quote[%file] /timer:0

return %?

:UpdateSelected

iff %@IsWindowsServer[] == 1 then
	call background update
endiff

return

