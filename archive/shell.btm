@echo off

REM Initialize
set TakeCommand=%@if[ %@WindowExist[TC 12*] == 1 ,tcmd.exe /r]

REM Argument defaults
set shell=tc
set user=
set password=
set interactive=
set host=

REM Arguments
:GetArgs
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

iff IsLabel %1Shell then
	set shell=%1
	shift
  goto GetArgs
endiff

iff "%1" == "/u" .or. "%1" == "/user" then
	set user=%2
	if "%user" == "admin" .or. "%user" == "root" set user=Administrator
	shift 2
  goto GetArgs
endiff

iff "%1" == "/p" .or. "%1" == "/passwd" .or. "%1" == "/password" then
	set password=%2
	shift 2
  goto GetArgs
endiff

iff "%1" == "interactive" then
	set interactive=-i
	shift
	goto GetArgs
endiff

iff "%1" == "/h" .or. "%1" == "/host" then
	if "%2" != "%ComputerName" set host=%2
	shift	2
	goto GetArgs
endiff

iff not defined shell .or. %# != 0 goto usage

REM Get information about the shell to start
set args=
set title=`%shell`
gosub %shell%Shell

REM btm batch files must are started with take command console
iff "%@ext[%program]" == "btm" then
	set args=/c "%program" %args
	set program=%@search[tcc]
endiff

REM Set the window title
iff defined user .and. defined host then
	set NewTitle=%shell (%user on %host)
elseiff defined user then
	set NewTitle=%shell (%user)
elseiff defined host then
	set NewTitle=%shell on %host
else
	set NewTitle=%shell
endiff

REM Run on the specified host as the specified user
iff defined host then
	
	iff "%@HostInfo[%host,platform]" == "win" then
	
		REM Prepare connection to host
		iff defined host then
			call host prepare %host
			if %? != 0 quit %?
		endiff
		
		start /pgm psexec.exe \\%host ^
			%@if[defined user,-u %user] ^
			%@if[defined password,-p %password] ^
			%interactive "%program" %args
			
		sleep 1
		activate "%_WinFgWindow" "%NewTitle" >& nul:
		call TakeCommand.btm attach

	REM Connect using PuTTY ssh for for non-windows hosts
	else
		call pu shell %host %user
		
	endiff
	
REM Run with a specific user - saving credentials if not in a domain (logon server needed in a domain)
elseiff defined user then
	RunAs.exe /user:%user %@if[ %@InDomain[] == 0 ,/SaveCred] "\"%program\" %args"
	sleep 1
	activate "%_WinFgWindow" "%NewTitle" >& nul:
	
else
	%TakeCommand "%program" %args
	if defined TakeCommand activate "%title" "%NewTitle" >& nul:
	
endiff

quit 0

:usage
text
shell [/u[ser] <user>] [/p[assword] <password>] [interactive] [/h[ost] <host>] [cmd|tc|ps|bash](tc)
endtext
quit 1

return

:CmdShell
:CommandShell
set shell=cmd
set program=cmd.exe
set title=*cmd.exe*
return

:TakeCommandShell
:TccShell
:TcShell
:4ntShell
set shell=tc
set program=tcc.exe
set title=*tcc.exe*
return

:PowerShellShell
:PowerShell
:PsShell
set shell=ps
set program=PowerShell.btm
return

:BashShell
set shell=bash
set program=bash.btm
return