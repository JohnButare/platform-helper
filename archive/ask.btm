@echo off

REM Initialize
on break cancel 1

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set message=%@UnQuote[%1]
if "%message" = "" goto usage
shift

set ValidResponses=ny
iff "%1" == "ValidResponses" then
	set ValidResponses=%2
	shift 2
endiff

set DefaultResponse=y
iff %# != 0 then
	iff "%DefaultResponse" == "none" then
		set DefaultResponse=
	else
		if %@len[%DefaultResponse] != 1 goto usage
		set DefaultResponse=%1
	endiff
	shift
endiff

set delay=
iff %# gt 0 .and. %@numeric[%1] == 1 then
	set delay=%1	
	shift
endiff

if %# != 0 goto usage

REM Allow enter key to be pressed if there is a default response
iff defined DefaultResponse then
	set ValidResponses=%ValidResponses[Enter]
endiff

REM Display the message
iff defined DefaultResponse then
	echos %message (%DefaultResponse)?` `
else
	echos %message?` `
endiff

REM Get the input
if defined delay set delay=/W%delay
set response=%DefaultResponse
inkey /c /k"%ValidResponses" %delay /x %%response

iff "%response" == "@28" then
  set response=%DefaultResponse
endiff
echo.

quit %@index[%ValidResponses,%response]

:usage
text
ask <message> [ValidResponses <valid responses>](YyNn) [DefaultResponse](y) [delay](infinite)
  message - message to display
  ValidResponses - valid response keys that will be accepted
  DefaultResponse - the default response, or none if there is no default response
  delay - maximum number of seconds to wait
	
  Return value is the index of the response in ValidResponse.  Defaults to 0=No 1=Yes
endtext
quit 0

:done
quit
