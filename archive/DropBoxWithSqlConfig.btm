@echo off
SetLocal
on break cancel 1

REM Initialize
set title=DropboxTrayIcon
set program=%AppData\Dropbox\bin\Dropbox.exe
set ConfigDb=%AppData\Dropbox\config.db

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=start
iff %# gt 0 then
  set command=%1
  shift
endiff
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text
usage: DropBox [start|startup|close](start)
  config [db|backup|sql <sql>](db) - manage configuration database
  proxy on|off|status - change proxy configuration
  MoveConfig <AppConfigDir> <DropBoxDir> - Move applications configuration files
    to DropBox.  AppConfigDir is the location of the application configuration files.
    DropBoxDir is the location to move the configuration files to in DropBox.
endtext
quit 2

:start
:startup
if not IsFile "%program" return
if %@IsTaskRunning[%@name[%program]] == 1 return 0
start /pgm "%program"
return

:close
if %@IsTaskRunning[%@name[%program]] == 0 return 0
process.exe -q %@FileName[%program] 5
return

:IsInstalled
return %@if[IsFile "%program",1,0]

:IsRunning

gosub IsInstalled
if %_? == 0 return 0

return %@IsTaskRunning[%program]

:MoveConfig

if %# != 2 goto usage

set src=%@UnQuote[%1]
set dest=%@UnQuote[%2]

REM Return if the configuration is already linked to DropBox
if %@IsLink["%src"] == 1 return 0

call ask `Do you want to move application configuration data to the cloud?` y
if %? == 0 return 1

REM If there is no configuration data on  DropBox move the local configuration data to DropBox
iff IsDir "%src" .and. not IsDir "%dest" then
	call MergeDir /rename "%src" "%dest"
endiff

REM If both the local and dropbox configuration data exist prompt to remove the local data
iff IsDir "%src" .and. IsDir "%dest" then

	call ask `Do you want to remove the existing local (non-cloud) configuration? ` y
	if %? == 0 return 1
	
	call DelDir "%src"
	if %? != 0 return %?
	
endiff

REM Create the DropBox configuration directory if needed
iff not IsDir "%dest" then
	MakeDir "%dest"
	if %? != 0 return %?
endiff

REM Link the DropBox configuration directory to the local directory
call MakeLink junction "%dest" "%src"
return %?

:config

set command=db

REM Arguments

iff %# gt 0 .and. IsLabel Config%1 then
	set command=%1
	shift
endiff

gosub Config%command
return %_?

:ConfigBackup
call bak "%ConfigDb"
return %?

:ConfigDb
SQLite3 "%ConfigDb"
return %?

:ConfigSql

REM Arguments
if %# == 0 goto usage
set sql=%$

echo %sql | SQLite3 "%ConfigDb"
return %?

:proxy

REM Arguments
set command=%1
shift

REM Run command
switch %command

case on
	set value=2

case off
	set value=1
		
case status
	set status=%@ExecStr[call dropbox config sql `select * from config where key="proxy_mode";` |^
		cut --delimiter=`|` --fields=2]
		
	echo.%@if[ "%status" == "2" ,on,off]
	
	return
	
default
	goto usage
	
endswitch

REM Change the proxy status if needed
iff "%command" != "%@ExecStr[DropBox proxy status]" then
	call DropBox close
	call DropBox config sql `update config set value=%value where key="proxy_mode";`
	call DropBox startup
endiff

return
