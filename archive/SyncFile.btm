@echo off

set syncImage=
set silent=

REM Arguments

iff "%1" == "silent" then
  set silent=silent
  shift
endiff

iff %# != 2 then 
  goto usage
endiff

set srcFile=%@replace[^",,%1]
if "%@instr[0,2,%srcFile]" == "\\" then
  set SrcMachine=%@field["\",2,%srcFile]  
else
  set SrcMachine=%computerName
endiff

set destFile=%@replace[^",,%2]
if "%@instr[0,2,%destFile]" == "\\" then
  set DestMachine=%@field["\",2,%destFile]  
else
  set DestMachine=%computerName
endiff

set srcSyncFile=%srcFile%_sync_to_%DestMachine%
set destSyncFile=%destFile%_sync_to_%SrcMachine

REM Check for machine availability and files.

call host prepare %SrcMachine
if %? != 0 return %?

iff not exist "%srcFile" then
  echo ERROR: The source file does not exist.
  quit 1
endiff

call host prepare %DestMachine
if %? != 0 return %?

iff not exist "%destFile" then
  echo ERROR: The destination file does not exist.
  quit 1
endiff

REM Compare files
diff "%srcFile" "%destFile" >& nul:
iff %? == 0 then
  goto done
endiff

REM cmd=replaceSrc, replaceDest, edit
set cmd=edit

iff exist "%srcSyncFile" then
  set syncAge=%@FileAge["%srcSyncFile"]
  
  iff %@FileAge["%destFile"] gt %syncAge .and. %@FileAge["%srcFile"] gt %syncAge then
    set cmd=edit
    
  elseiff %@FileAge["%destFile"] gt %syncAge .and. %@FileAge["%srcFile"] lt %syncAge then
    set cmd=replaceSrc
    
  elseiff %@FileAge["%srcFile"] gt %syncAge .and. %@FileAge["%destFile"] lt %syncAge then
    set cmd=replaceDest
    
  endiff
  
endiff

iff %cmd == edit .and. defined silent then
  echo Editing required in silent mode, exiting.
  quit 0
endiff

REM Src file changing
iff %cmd == edit .or. %cmd == replaceSrc then
  call bak "%srcFile"
endiff

REM Dest file changing
iff %cmd == edit .or. %cmd == replaceDest then
  call bak "%destFile"
endiff

gosub %cmd

goto done

:edit

text

Comapre the files (F2) and edit until identical.  
endText

call emacs "%srcFile" "%destFile"
pause 

diff "%srcFile" "%destFile" >& nul:
iff %? != 0 then
  goto edit
endiff

return

:replaceSrc
echo REPLACING: Older source file.
copy "%destFile" "%srcFile"
return

:replaceDest
echo REPLACING: Older destination file.
copy "%srcFile" "%destFile"
return

:usage
echo USAGE: %0 [silent] src dest
echo If silent is specied, no keyboard echo input is required and 
echo conflicting files are not edited.
quit 1

:done

REM If the files are now the same, update sync files with current time
diff "%srcFile" "%destFile" >& nul:
iff %? == 0 then

  REM Update local sync file
  touch /c /q "%srcSyncFile"
  attrib /q +h "%srcSyncFile"
  
  REM Update remote sync file
  touch /c /q "%destSyncFile"
  attrib /q +h "%destSyncFile"

  REM Delete emacs backup files
  del /q /e "%srcFile~"
  del /q /e "%destFile~"
  
endiff

quit 0