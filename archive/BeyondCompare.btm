@echo off
SetLocal
on break cancel 1

REM Initialize
set program=%programs32\Beyond Compare 3\BCompare.exe
set ProgramMultiple=%programs32\Beyond Compare 3\BComp.exe
set ProgramCommand=%programs32\Beyond Compare 3\BComp.com

REM Profile
set ProfileApp=BeyondCompare
set ProfileMethod=%program
set ProfileSaveExtension=bcpkg

REM Arguments
if %@IsHelpArg[%@UnQuote[%1]] == 1 goto usage

set command=start
iff IsLabel %1 then
  set command=%1
  shift
endiff
if not IsLabel %command goto usage

REM Run command
gosub %command
quit %_?

:usage
text
usage: program [start [/pause]|close|command](start)
  command: run the command line version
  profile dir|SaveDir|backup|restore [<profile name>|default](latest)
endtext
quit 2

:profile
call profile.btm %$
if %? == 1 goto usage
EndLocal /d
return %?

:command
gosub IsInstalled & if %_? == 0 return 0
"%ProgramCommand" %$
return

:startup
gosub IsInstalled & if %_? == 0 return 0
gosub IsRunning & if %_? == 1 return 0
start /pgm "%ProgramMultiple" %$
return %?

:start
gosub IsInstalled & if %_? == 0 return 0
start /pgm "%ProgramMultiple" %$
if "%1" == "/pause" pause
return %?

:close
gosub IsRunning & if %_? == 0 return 0
process.exe -q %@FileName[%program]
return %?

:IsInstalled
return %@if[IsFile "%program",1,0]

:IsRunning
gosub IsInstalled & if %_? == 0 return 0
return %@IsTaskRunning[%program]